== 性能优化

虽然Starling模仿Flash的经典显示列表，但幕后的功能是完全不同的。
要实现最佳性能，您必须了解其架构的一些关键概念。
以下列表是您可以遵循的最佳做法，让您的游戏尽可能快地运行。

=== 普通AS3程序通用提示

==== 总是构建发行版本

首先也是最重要的一条：在测试性能时始终构建发行(release)版本。
与传统的Flash项目不同，当您使用Stage3D框架时，发行版本会产生巨大的差异。
根据您正在开发的平台不同，运行时速度差异可能非常大;发行版帧率甚至可以轻松达到调试版帧率的倍数。

* 在Flash Builder中，通过单击菜单：工程 [导出发行版本] 构建发布版本。
* 在Flash Develop中，选择“发布”配置并构建项目;
  然后在执行“PackageApp.bat”脚本时选择“ipa-ad-hoc”或“ipa-app-store”选项。
* 在IntelliJ IDEA中，选择菜单：构建 [AIR应用程序包] ;为Android选择“发布”，iOS选择“临时发行”。
  对于非AIR项目，请取消选择模块编译器选项中的“生成可调试SWF”。
* 如果您从命令行构建Starling项目，请确保`-optimize`为true，`-debug`为false。

.不要因此Flash Builder对话框而感到困惑。
image::not-release-build.png[Flash Builder Dialog]

==== 检查你的硬件

确保Starling确实使用GPU进行渲染。
这很容易检查：如果“Starling.current.context.driverInfo”包含字符串“software”，则Stage3D处于软件回退模式，否则使用GPU。

此外，一些移动设备可以在省电模式中运行。
进行性能测试时，请务必将其关闭。

==== 设置帧速率

无论您怎么优化,您的应用程序仍然以每秒24帧运行？
那么你可能从来没有设置你想要的帧速率，你看到只是Flash Player的默认设置。

要改变这一点，可以在启动类中使用相应的元数据标签，或者在Flash运行阶段手动设置帧速率。

[source, as3]
----
[SWF(frameRate="60", backgroundColor="#000000")]
public class Startup extends Sprite
{ /* ... */ }

// 或其他任何地方
Starling.current.nativeStage.frameRate = 60;
----

==== 使用Adobe Scout

Adobe Scout不仅对“memory_management_scout，内存分析”有用;它在性能分析方面同样强大。

它允许您实际查看每个ActionScript（和Starling）方法中花费了多少时间。
这是非常有用的，因为它显示了您可以从任何优化中获得最大收益的位置。
没有它，你最终优化的代码可能会在实际上与帧速率无关的区域！

TIP: 记住，过早优化是所有不幸的根源！

与传统的分析器相比，它的优点在于，它还可以在发布模式下运行，所有优化都已做到位。
这确保其输出非常准确。

==== 异步解码加载的图像

默认情况下，如果您使用Loader加载PNG或JPEG图像，则图像数据不会立即解码，而是首次使用时。
这种情况发生在主线程上，可能会导致应用程序在纹理创建时表现卡顿。
为了避免这种情况，请将图像解码策略标志设置为“ON_LOAD”。
这将导致图像在Loader的后台线程中直接解码。

[source, as3]
----
loaderContext.imageDecodingPolicy = ImageDecodingPolicy.ON_LOAD;
loader.load(url, loaderContext);
----

另一方面，您可能正在使用Starling的AssetManager加载纹理，不是吗？
在这种情况下，不要担心，它内部已经采用这种做法。

==== 避免 "for each"

当使用经常重复或深入嵌套的循环时，最好避免“for each”而改用经典的“for i”优化性能： http://jacksondunstan.com/articles/358[better] 。
此外，请注意，每次循环都会执行循环条件一次，因此将其保存到一个额外的变量中速度更快。

[source, as3]
----
// 慢:
for each (var item:Object in array) { ... }

// 更快:
for (var i:int=0; i<array.length; ++i) { ... }

// 最快:
var length:int = array.length;
for (var i:int=0; i<length; ++i) { ... }
----

==== 避免分配内存

避免创建大量临时对象。
它们占用内存，需要被垃圾收集器清理，这可能会在运行时产生小的卡顿。

[source, as3]
----
// 较差:
for (var i:int=0; i<10; ++i)
{
    var point:Point = new Point(i, 2*i);
    doSomethingWith(point);
}

// 更好:
var point:Point = new Point();
for (var i:int=0; i<10; ++i)
{
    point.setTo(i, 2*i);
    doSomethingWith(point);
}
----

实际上，Starling包含一个帮助类：Pool。
它提供了对象池,能存取经常需要的对象，如Point，Rectangle和Matrix。
您可以从该池中“借”对象，并在完成后返还到池中。

[source, as3]
----
// best:
var point:Point = Pool.getPoint();
for (var i:int=0; i<10; ++i)
{
    point.setTo(i, 2*i);
    doSomethingWith(point);
}
Pool.putPoint(point); // 不要忘记这个！
----

=== Starling特性提示

==== 最大限度地减少状态变化

你已经知道，Starling使用Stage3D渲染显示列表。
这意味着所有绘图都是由GPU完成的。

现在，Starling可以一个接一个地将四边形发送到GPU，逐一绘制。
事实上，最初的Starling发行版就是这样工作的！
但是，为了获得最佳性能，GPU更适合获得一大堆数据，同时绘制所有数据。

这就是为什么更新的Starling版本在将它们发送到GPU之前,将尽可能多的四边形拼凑在一起的原因。
不过，它只能批量渲染具有相似属性的四边形。
每当遇到具有不同“状态”的四边形时，GPU将发生“状态改变”，并绘制当前批次的四边形。

[NOTE]
====
我在本节中使用四边形(Quad)和图像(Image)同义。
记住，Image只是Quad的一个子类，只是它添加了几个方法。
此外，Quad扩展网格(Mesh)，并且以下内容也适用于Mesh。
====

这些是构成状态的关键属性：

* 纹理 (不同的纹理来自同一图集)
* 显示对象的blendMode(混合模式)
* Mesh/Quad/Image的textureSmoothing(平滑)值
* Mesh/Quad/Image的textureRepeat(重复填充)值

如果您以尽可能少的更改状态的制作方式设置场景，则您的渲染性能将获得巨大的提升。

Starling的统计显示又提供了有用的数据。
它显示每帧执行多少次绘制调用。
应用程序状态更改次数越多，这个数字就越高。

.统计显示包括当前绘制次数。
image::stats-display.png[Statistics Display]

[NOTE]
====
统计显示小界面本身也会引发绘制调用。
但是，Starling已经将这个因素考虑在内，自身显示导致的绘制计数也一并统计,以得出最终明确的结论。
====

您的目标应始终保持绘制调用次数尽可能低。
以下提示将告诉你如何做到。

===== 画家算法

要了解如何将状态更改次数降到最低，您需要知道Starling处理对象的逻辑顺序。

像Flash一样，Starling使用画家算法来处理显示列表。
这意味着它像画家一样绘制您的场景：从底层的对象（例如背景图像）开始并向上移动，在先前的对象之上绘制新对象。

.用画家算法绘制场景。
image::painters-algorithm.png[Painter's algorithm]

如果您在Starling中设置了这样一个场景，您创建了三个精灵：一个包含远处的山脉，一个包含地面，另一个包含植被。
山脉处于底部（在_children中下标为0），植被处于顶部（下标2）。
每个精灵都将包含实际图像。

.从上往下看的景观图。
image::landscape.png[Landscape Scene Graph]

在渲染时，Starling将从左侧开始，以“山 1”开始，向右移动，直到达到“树 2”。
如果所有这些对象具有不同的状态，这意味着会产生六次绘制调用。
如果每个对象的纹理都是从单独的位图加载就一定会发生这种情况。

// <note tip>
// Another tool at your disposal is the [[http://doc.starling-framework.org/core/starling/display/DisplayObjectContainer.html#sortChildren()|DisplayObjectContainer::sortChildren()]] method which can be used to sort layers, within a ''Sprite'' object for example, based on properties such as ''x'', ''y'', ''alpha'' etc. The method accepts a compare function which means you can sort objects based on any criteria you wish! :-D
// </note>

===== 纹理图集

如果所有这些纹理都是从同一个图集中加载，Starling将能够一次绘制所有对象！
这就是为什么纹理图集是如此重要的原因之一。
(上面列出的影响状态的属性不改变的情况下)

.相同的场景图，现在使用单一的图集纹理。
image::landscape-2.png[Landscape Scene Graph 2]

这个实验结果指出我们应该总是把纹理封装到一个图集后使用。
这里，每个图像来源于同一个图集（由具有相同颜色描绘的所有节点）。

有时候，并不是所有的纹理都适合存放于同一图集。
因为纹理图集的尺寸是有上限的，所以你迟早会耗尽空间。
但这并不是问题，只要你以一种聪明的方式安排你的纹理。

.对象的顺序有所不同。
image::landscape-3.png[Landscape Scene Graph 3]

这两个例子都使用两个图集（每个图集一个颜色）。
但是，当左侧的显示列表强制对每个对象进行状态更改时，右侧的版本将能够在两个批次中完成所有对象的绘制。

==== 使用MeshBatch类

一次绘制大量四边形或其他网格的最快方法是使用MeshBatch类。
这是Starling内部所有渲染都会使用的类，所以它是经过重度优化的。.footnote:[如果您仍在使用Starling 1.x，请改用“QuadBatch”。]
它的工作原理如下：

[source, as3]
----
var meshBatch:MeshBatch = new MeshBatch();
var image:Image = new Image(texture);

for (var i:int=0; i<100; ++i)
{
    meshBatch.addMesh(image);
    image.x += 10;
}

addChild(meshBatch);
----

你注意到了吗？您可以随意添加相同的图像！
此外，添加对象是一个非常快的操作;例如不会调度任何事件（而将对象添加到容器时则会派发事件）。

正如预想的那样，这个机制也存在一些缺点：

* 您添加的所有对象必须具有相同的状态（例如：使用来自同一图集的纹理）。
  您添加到MeshBatch的第一张图像将决定其状态。
  您以后不能更改状态，除非将其完全重置。
* 您只能添加Mesh类或其子类（包括Quad，Image，甚至MeshBatch）的实例。
* 对象删除非常棘手：只能通过修剪MeshBatch内部的顶点和索引数来删除网格。
  但是，您也可以在特定索引处覆盖网格。

由于这些原因，它仅适用于非常具体的用例（例如，BitmapFont类，其内部就使用了MeshBatch）。
在这些情况下，这绝对是最快的选择。您在Starling中找不到更有效的渲染大量物件的方式。

==== TextFields批处理

默认情况下，即使您的字形纹理是主要纹理图集的一部分，TextField也需要单独的一次绘图调用。
这是因为长文本需要大量的CPU计算以达到批次的目的;这种情况下只需简单地绘制它们即可（不需要将它们复制到MeshBatch）。

但是，如果文本字段只包含少数几个字符（经验法则：16个字符以下），则可以在TextField上启用`batchable`属性。
启用该功能后，您的文本将像其他显示对象一样能进行合并批渲染。

==== 使用BlendMode.NONE

如果您有完全不透明的矩形纹理，可以通过禁用这些纹理的混合功能来节省GPU消耗。
这对于大背景图像特别有用。

[source, as3]
----
backgroundImage.blendMode = BlendMode.NONE;
----

当然这也意味着产生了一个额外的状态变化，即多一次绘制调用，所以不要过度使用这种技术。
对于小图像，它可能不值得的这样做（除非它们由于其他原因导致状态已经改变）。

==== 使用stage.color

通常情况下，您的游戏中看不到舞台的实际颜色，因为舞台上总是有图像或网格。

在这种情况下，始终将其设置为黑色（“0x0”）或白色（“0xffffff”）。
因为在某些移动硬件上调用“context.clear”，对于全部为1或全部为0的情况，似乎存在一个快速的硬件优化途径。
一些开发人员测试对比帧使用时间，发现即使这样一个简单的改变也有一个非常好的增益！

[source, as3]
----
[SWF(backgroundColor="#0")]
public class Startup extends Sprite
{
    // ...
}
----

另一方面，如果您的游戏背景是纯色，您也可以利用这一点：只需将舞台颜色设置为该值，而不是显示图像或彩色四边形。
任何情况下，Starling必须每帧清除一次舞台 - 因此，如果您更改舞台颜色，则该操作则不会有任何消耗。

[source, as3]
----
[SWF(backgroundColor="#ff2255")]
public class Startup extends Sprite
{
    // ...
}
----

==== 避免查询宽度和高度

查询“宽”和“高”属性比你想象的更耗费性能，特别是在精灵上查询这些属性。
必须计算矩阵，并且每个子对象的每个顶点将与该矩阵相乘。

为此，请避免重复访问它们。例如，在一个循环中反复访问它们。
在某些情况下，使用常量值更有价值。

[source, as3]
----
// 较差:
for (var i:int=0; i<numChildren; ++i)
{
    var child:DisplayObject = getChildAt(i);
    if (child.x > wall.width)
        child.removeFromParent();
}

// 较好:
var wallWidth:Number = wall.width;
for (var i:int=0; i<numChildren; ++i)
{
    var child:DisplayObject = getChildAt(i);
    if (child.x > wallWidth)
        child.removeFromParent();
}
----

==== 禁用容器touchable功能

当您将鼠标/手指移动到屏幕上时，Starling必须找出哪个对象被选中。
这可能是一个非常耗性能的操作，因为它需要对每个显示对象进行碰撞测试（在最坏的情况下）。

因此，无论如何，如果你不关心他们被选中，禁用其touchable功能将有助于提高运行时性能。
最好在完整的容器上禁用touchable：因为这样，Starling也就不必对其孩子进行迭代。

[source, as3]
----
// 好:
for (var i:int=0; i<container.numChildren; ++i)
    container.getChildAt(i).touchable = false;

// 更好:
container.touchable = false;
----

==== 隐藏在舞台边界之外的对象

Starling将发送显示列表的所有对象到GPU。
即使此对象在舞台界限之外，也是如此！

你可能会想：为什么Starling不自动忽略那些看不见的物体？
原因是以普通的方式检查可视性(CPU实时计算)是相当昂贵的。
事实上，与其这样做昂贵的CPU操作，不如将对象发送到GPU，GPU剪辑速度更快。
GPU实际上是非常高效的，如果对象在屏幕边界之外，则很快就会中止整个渲染管道。

但是，上传数据还需要时间，您可以避免这种情况的话更好。
在应用程序具体模块内或游戏内部逻辑中，通常更容易进行可见性检查（例如，您仅仅将x / y坐标与常量进行比较就可以做到）。
如果你有很多对象在这些边界之外，这是值得的。
从显示列表上移除这些元素，或者将它们的`visible`属性设置为`false`。

==== 利用事件池

与传统Flash相比，Starling增加了一种事件调度的附加方法：

[source, as3]
----
// 经典方式：
object.dispatchEvent(new Event("type", bubbles));

// 新方式：
object.dispatchEventWith("type", bubbles);
----

新方式将像第一个方法一样调度事件对象，但在幕后，它将把您的事件对象缓存进对象池。
这意味着你不必浪费垃圾回收器来回收事件对象。

换句话说，写代码较少且更快 - 因此，这是调度事件的首选方式。
(除了需要调度“Event”的自定义子类，不能使用该方法进行调度以外。)
