== 自定义样式

现在我们已经发掘了Stage3D的原始力量，让我们继续沿着这条道路前进！
在本节中，我们将编写一个简单的网格样式。
在Starling 2中，所有渲染都通过样式完成;通过创建自己的网格样式，您可以创建特殊效果，而不必担心带来任何性能损失。

NOTE: 在继续之前，请确保您已经阅读过<<自定义滤镜>>那一节内容。
滤镜和样式存在许多类似的概念，所以从两个更简单的例子开始是有意义的。
下面，我假设你已经熟悉在相关章节介绍过的所有内容。

=== 目标

目标是：最终效果和我们用ColorOffsetFilter得到的一样!我们希望为每个像素的颜色值添加一个偏移量。
只是这一次，我们用网格样式来达到目的！我们将它称为... ColorOffsetStyle。

.应用颜色偏移样式。
image::customfilter-offset.png[Offset with a Style]

在继续之前，了解滤镜和样式之间的区别至关重要。

==== 滤镜与样式

如前所述，滤镜是像素级的工作：对象被渲染为纹理，然后滤镜以某种方式处理该纹理。
另一方面，样式可以访问对象的所有原始几何数据，或者更精确地说：访问对象的顶点。

虽然样式在某些方面受到了限制（例如，你不能通过样式实现模糊效果），但样式工作起来更高效。
首先，因为你不需要将对象绘制到纹理这一步。
第二，更重要的是：网格样式可以被批次渲染。

如你所知，保持较低的绘制调用的次数对于高帧率运行是非常重要的。
为了确保发生这种情况，Starling在绘制之前将尽可能多的把对象集中在一起。
问题是，如何决定把哪些对象分配到同一个批次？
这也是网格样式发挥作用的地方：只有具有相同样式的对象可以分批在一起。

如果您向舞台添加了三个应用了ColorOffsetFilter的图片，您将看到至少三次绘图调用。
同样是这三个对象，而改用ColorOffsetStyle，就只需要一次绘制调用。
虽然网格样式写起来并不是那么简便 - 但这个努力是值得付出的！

=== 扩展网格样式

所有网格样式的基类是`starling.styles.MeshStyle`。
这个类提供了我们需要的所有基础功能。让我们先看一个示例：

[source, as3]
----
public class ColorOffsetStyle extends MeshStyle
{
    public static const VERTEX_FORMAT:VertexDataFormat =
            MeshStyle.VERTEX_FORMAT.extend("offset:float4");

    private var _offsets:Vector.<Number>;

    public function ColorOffsetStyle(
        redOffset:Number=0, greenOffset:Number=0,
        blueOffset:Number=0, alphaOffset:Number=0):void
    {
        _offsets = new Vector.<Number>(4, true);
        setTo(redOffset, greenOffset, blueOffset, alphaOffset);
    }

    public function setTo(
        redOffset:Number=0, greenOffset:Number=0,
        blueOffset:Number=0, alphaOffset:Number=0):void
    {
        _offsets[0] = redOffset;
        _offsets[1] = greenOffset;
        _offsets[2] = blueOffset;
        _offsets[3] = alphaOffset;

        updateVertices();
    }

    override public function copyFrom(meshStyle:MeshStyle):void
    {
        // 待实现
    }

    override public function createEffect():MeshEffect
    {
        return new ColorOffsetEffect();
    }

    override protected function onTargetAssigned(target:Mesh):void
    {
        updateVertices();
    }

    override public function get vertexFormat():VertexDataFormat
    {
        return VERTEX_FORMAT;
    }

    private function updateVertices():void
    {
        // 待实现
    }

    public function get redOffset():Number { return _offsets[0]; }
    public function set redOffset(value:Number):void
    {
        _offsets[0] = value;
        updateVertices();
    }

    // 其他偏移属性需要相应地实现。

    public function get/set greenOffset():Number;
    public function get/set blueOffset():Number;
    public function get/set alphaOffset():Number;
}
----

这是我们的出发点。
你会看到，已经比我们上次看到的最后一个初始滤镜类的例子多出了很多代码。
让我们来看看代码的各个部分。

==== 顶点格式

第一件值得注意的事：位于类顶部的顶点格式常量。
我已经提到样式是顶点级别的工作，让您访问对象的所有几何数据。
VertexData类存储该几何数据，但我们从未实际讨论过该类具体把几何数据存放在哪里，以及是如何存放的。
这是由VertexDataFormat定义的。

MeshStyle使用的默认格式如下：

  position:float2, texCoords:float2, color:bytes4

这个字符串的语法看起来应该是熟悉的;它是具有某些数据的属性列表

* `position`属性存储两个浮点（顶点的x和y坐标）。
* `texCoords`属性存储两个浮点数（顶点的纹理坐标）。
* `color`属性存储顶点颜色的四个字节（每个通道一个字节）。

具有此格式的VertexData实例将为网格的每个顶点存储这些属性，属性存放的顺序与上述字符串显示的完全一致。
这意味着每个顶点将占用20个字节（8 + 8 + 4）。

当创建网格并且没有特别指定任何样式时，将交由默认的MeshStyle来渲染，顶点格式则采用上述标准格式。
你绘制纹理或彩色网格,所有必要的信息都存放在这里了。

但是对于我们的ColorOffsetStyle，这还不够：我们还需要存储我们的颜色偏移。
因此，我们需要定义一个新的格式，它添加一个由四个浮点值组成的“offset”属性。

[source, as3]
----
MeshStyle.VERTEX_FORMAT.extend("offset:float4");
// => position:float2, texCoords:float2, color:bytes4, offset:float4
----

现在，你可能会问：为什么需要这么做？滤镜没有自定义顶点格式，不也是能正常工作吗？

这是一个很好的问题，我很高兴你问！
答案在于Starling的批处理代码。
当我们将样式分配给一些后续的网格时，它们将被分组在一起批处理 - 这是我们做这个努力的全部原因，对吧？

但是批处理是什么意思？
它意味着我们将所有单个网格的顶点复制到一个更大的网格并渲染。
在Starling的渲染内部，你会发现看起来像这样的代码：

[source, as3]
----
var batch:Mesh = new Mesh();

batch.add(meshA);
batch.add(meshB);
batch.add(meshC);

batch.style = meshA.style; // ← !!!
batch.render();
----

你看到了的问题的实质吗？大网格（“batch”）的网格样式，即是第一个被添加的网格样式的副本!
虽然这三种样式可能会使用不同的设置。
如果这些设置只存储在style中，除了一个之外的所有设置将在渲染时丢失。
所以，必须将样式数据存储在其目标网格的VertexData中！
只有这样，大的batch网格才能单独接收所有偏移量。

IMPORTANT: 由于这一点是如此重要！我再复述一遍：
*样式的设置必须始终存储在目标网格的顶点数据中。*

按照惯例，顶点格式总是作为样式类中的静态常量，访问时由`vertexFormat`属性返回。
当样式分配给网格时，其顶点将自动适应该新格式。

当你理解了这个概念，你就能理解这一切。
剩下的就是更新代码，以便从顶点数据而不是片段常量读取偏移量。

我讲的可能有些过头了。

==== 成员变量

你会注意到，即使我坚持要将所有数据存储在顶点中，仍然有一组偏移存储在成员变量中：

[source, as3]
----
private var _offsets:Vector.<Number>;
----

这是因为我们希望开发人员能够在将样式分配给网格之前对其进行配置。
即便没有目标对象，没有顶点数据，我们也能存储这些偏移，对吧？
所以我们使用这个向量。
一旦分配了目标，值就被复制到目标的顶点数据（参见`onTargetAssigned`）。

==== copyFrom

在批处理期间，样式有时必须从一个实例复制到另一个实例（主要是为了能够重用它们而不会给垃圾回收器带来压力）。
因此，有必要重写`copyFrom`方法。
我们这样做：

[source, as3]
----
override public function copyFrom(meshStyle:MeshStyle):void
{
    var colorOffsetStyle:ColorOffsetStyle = meshStyle as ColorOffsetStyle;
    if (colorOffsetStyle)
    {
        for (var i:int=0; i<4; ++i)
            _offsets[i] = colorOffsetStyle._offsets[i];
    }

    super.copyFrom(meshStyle);
}
----

直接了当：我们只是检查复制的样式是否类型正确，然后在当前实例上复制它的所有偏移。
剩下的事都交给超类完成。

==== createEffect

这看起来很熟悉，对吧？

[source, as3]
----
override public function createEffect():MeshEffect
{
    return new ColorOffsetEffect();
}
----

它的工作原理跟滤镜类相似;我们返回稍后将要创建的ColorOffsetEffect。
但是，它与滤镜使用方式不同（因为偏移值是从顶点读取的），但是可以创建一个适用于两者的效果。

==== onTargetAssigned

如上所述，我们需要将偏移存储在目标网格的顶点数据中。
是的，这意味着偏移值在每个顶点中都存在一份拷贝，即使这似乎有点浪费。
但这是保证样式支持批处理的唯一方法。

当滤镜被分配一个目标时，这个回调将被执行 - 根据这条线索我们知道顶点被更新了。
同样的工作我们可能需要在别处再做一次，所以我把实际的处理过程封装到`updateVertices`方法中。

[source, as3]
----
override protected function onTargetAssigned(target:Mesh):void
{
    updateVertices();
}

private function updateVertices():void
{
    if (target)
    {
        var numVertices:int = vertexData.numVertices;
        for (var i:int=0; i<numVertices; ++i)
            vertexData.setPoint4D(i, "offset",
                _offsets[0], _offsets[1], _offsets[2], _offsets[3]);

        setRequiresRedraw();
    }
}
----

你可能想知道`vertexData`对象来自哪里。
一旦目标被分配，`vertexData`属性将引用目标的顶点（样式本身从不拥有任何顶点）。
因此，上面的代码循环遍历目标网格的所有顶点，并分配正确的偏移值，准备在渲染期间使用。

=== 扩展MeshEffect

现在我们已经完成了样式类 - 是时候讨论效果了，这是实际渲染发生的地方。
这一次，我们要扩展MeshEffect类。
记住，效果类简化了低级渲染代码的编写。
我实际上谈论一组具有以下继承关系的类：

[plantuml, effect-classes]
....
include::../defaults.plantuml[]

Effect <|-- FilterEffect
FilterEffect <|-- MeshEffect
....

基类（Effect）简洁到了极致：它只绘制白色三角形。
FilterEffect添加了对纹理的支持，MeshEffect用于颜色和alpha。

NOTE: 这两个类也可以命名为TexturedEffect和ColoredTexturedEffect，但我选择关于用他们用法的命名方式。
如果你创建一个自定义滤镜，你需要扩展FilterEffect;如果你创建一个自定义网格样式，则需要扩展MeshEffect。

让我们来看一下ColorOffsetEffect的设置，我们稍后会介绍一些代码片断。

[source, as3]
----
class ColorOffsetEffect extends MeshEffect
{
    public  static const VERTEX_FORMAT:VertexDataFormat =
        ColorOffsetStyle.VERTEX_FORMAT;

    public function ColorOffsetEffect()
    { }

    override protected function createProgram():Program
    {
        // TODO
    }

    override public function get vertexFormat():VertexDataFormat
    {
        return VERTEX_FORMAT;
    }

    override protected function beforeDraw(context:Context3D):void
    {
        super.beforeDraw(context);
        vertexFormat.setVertexBufferAt(3, vertexBuffer, "offset");
    }

    override protected function afterDraw(context:Context3D):void
    {
        context.setVertexBufferAt(3, null);
        super.afterDraw(context);
    }
}
----

如果你把它和上一个教程的模拟滤镜效果类进行比较，你会看到所有的'offset`属性被删除了;而我们现在覆盖`vertexFormat`，它确保我们使用的格式与指定样式相同，准备好每个顶点存储我们的偏移值。

==== beforeDraw & afterDraw

`beforeDraw`和`afterDraw`-方法现在配置上下文，以便我们可以从着色器中读取offset属性为`va3`（vertex属性序号为3）。
让我们来看看`beforeDraw`中的那行：

[source, as3]
----
vertexFormat.setVertexBufferAt(3, vertexBuffer, "offset");
----

这等价于以下这行代码：

[source, as3]
----
context.setVertexBufferAt(3, vertexBuffer, 5, "float4");
----

第三个参数（`5→bufferOffset`）表示顶点格式中颜色偏移的位置，最后一个参数（`float4→format`）是该属性的格式。
所以我们不必计算和记住这些值，我们可以要求`vertexFormat`对象为我们设置这个属性。
这样，如果格式发生变化，我们就会在偏移之前添加另一个属性，代码将继续工作。

顶点缓冲区属性应该在绘制完成时始终清除，因为以下绘制调用可能使用不同的格式。
这就是我们在`afterDraw`方法中所做的。

==== createProgram

是时候介绍样式底层最核心的部分了;实际渲染的AGAL代码。
这一次，我们还要实现顶点着色器;它不会使用标准实现，因为我们需要添加一些自定义逻辑。
但是，片段着色器几乎与我们为滤镜写的一样。
让我们来看看！

[source, as3]
----
override protected function createProgram():Program
{
    var vertexShader:String = [
        "m44 op, va0, vc0", // 将4x4变换矩阵输出到剪裁空间
        "mov v0, va1     ", // 将纹理坐标传递给片段程序
        "mul v1, va2, vc4", // 将alpha（vc4）与颜色（va2）相乘，并将结果复制到v1
        "mov v2, va3     "  // 将颜色偏移量复制到v2
    ].join("\n");

    var fragmentShader:String = [
        tex("ft0", "v0", 0, texture) +  // 从纹理获取颜色
        "mul ft0, ft0, v1",             // 将颜色与纹理颜色相乘
        "mov ft1, v2",                  // 将完整偏移量复制到ft1
        "mul ft1.xyz, v2.xyz, ft0.www", // 将颜色偏移的rgb分量与alpha相乘 (pma!)
        "add oc, ft0, ft1"              // 将纹理颜色值与偏移量相加，并将计算结果输出到oc
    ].join("\n");

    return Program.fromSource(vertexShader, fragmentShader);
}
----

要理解顶点着色器正在做什么，你首先要了解它的输入工作方式。

* `va`（vertex attribute 缩写，即顶点属性）寄存器包含当前顶点的属性，取自顶点缓冲区。
  它们的排序与顶点格式中的属性排序完全一样，我们在前面已设置：`va0`是顶点位置，`va1`是纹理坐标，`va2`是颜色，`va3`是偏移量。
* 对于所有的顶点，两个常量是相同的：`vc0-3`包含modelview-projection矩阵，`vc4`是当前的alpha值。

任何顶点着色器的主要任务是将顶点位置移动到所谓的“剪裁空间”。
这是通过将顶点位置乘以`mvp矩阵`（modelview-projection matrix）来实现的。
顶点着色器第一行就是做这个事情的，在Starling中，无论在哪一个顶点着色器，你都能够发现它。
它负责找出顶点在屏幕上的位置。

另一方面，我们或多或少会通过变量寄存器‘v0’ - ‘v2’ 转移数据到片段着色器。

片段着色器几乎是与滤镜类相同的复制品。
你能找到区别吗？
我们正是从这种寄存器中读取颜色偏移量的。 之前，它存储在一个常数中，而现在,存储在变量寄存器`v2`中。

=== 尝试

有了它，我们几乎完成了我们的样式！
让我们来测试一下。
一个真正大胆的举动，我们立即把它应用在两个对象上，看看批处理是否正确工作。

[source, as3]
----
var image:Image = new Image(texture);
var style:ColorOffsetStyle = new ColorOffsetStyle();
style.redOffset = 0.5;
image.style = style;
addChild(image);

var image2:Image = new Image(texture);
image2.x = image.width;
var style2:ColorOffsetStyle = new ColorOffsetStyle();
style2.blueOffset = 0.5;
image2.style = style2;
addChild(image2);
----

.两个自定义样式的图像，仅用一次绘制调用呈现。
image::custom-style-sample.png[Custom Style Sample]

天啦噜，它正确的工作了！
一定要看看左上角的绘制调用(drawCall)，一个诚实和恒定的“1”。

有一个小小的事情还要做;我们创建了上面的着色器，是假定总是可以通过某一个纹理来读取颜色数据。
然而，样式也可能被分配给一个不使用任何纹理的网格，所以我们必须为这种情况编写一些特定的代码（这是很简单，我不打算再详细说明）。

完整的类，包括解决了上述最后一个问题的参考示例，可以在这里找到： https://gist.github.com/PrimaryFeather/eaaa4a1b50cf546613d2[ColorOffsetStyle.as].

=== 去往何方

这就是我们的样式！
我希望你和我一样激动，我们成功完成了我们的任务。
你上面看到的是扩展Starling的关键，想象力有多大，扩展能力就有多大!
MeshStyle类还有一些其他的技巧，所以一定要阅读完整的类文档。

我很期待看到你们的让人惊艳的作品!
