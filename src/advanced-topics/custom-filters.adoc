== 自定义滤镜

你准备好开始行动了吗？
现在,我们正在进入自定义渲染代码的领域，让我们从一个简单的片段过滤器开始。

是的，这将涉及一些低级代码; 嘿嘿，你甚至需要写几行汇编程序！
但不要害怕，这不是火箭科学。
正如我的老数学老师曾经说过的：一个经过训练的猴子都能做到！

NOTE: 记住：滤镜是针对显示对象的每一个像素工作(换个说法:像素级)。
已过滤的对象将被渲染到纹理，然后通过自定义片段着色器（因此名称为fragment filter）进行处理。

=== 目标

我们选择一个尽量简单的目标，但它至少应该是一个有用的，对吧？
那就让我们创建一个ColorOffsetFilter(颜色偏移滤镜)吧。

你可能知道,可以通过给一个网格的任何顶点指定一种颜色来达到目的。
在渲染时，颜色将与纹理颜色相乘，这提供了一种非常简单（和快速）的方式来修改纹理的颜色。

[source, as3]
----
var image:Image = new Image(texture);
image.color = 0x808080; // R = G = B = 0.5
----

背后的数学非常简单：在GPU上，每个颜色通道（红色，绿色，蓝色）由0和1之间的值表示。
例如纯红色表示如下：

  R = 1, G = 0, B = 0

在渲染时，该颜色将乘以纹理的每个像素的颜色（也称为“纹素”）。
图像颜色的默认值为纯白色，意味着所有通道值都是1。
因此，纹理颜色看起来不变（与`1`的乘法结果是无操作）。
当你分配一个不同的颜色，经过乘法计算后将产生一种新的颜色，例如。

  R = 1,   G = 0.8, B = 0.6  ×
  B = 0.5, G = 0.5, B = 0.5
  -------------------------
  R = 0.5, G = 0.4, B = 0.3

这里有问题：这只会使对象变得更暗，而不是更亮。
这是因为我们只能乘以0和1之间的值;0意味着结果将是黑色的，而1意味着它保持不变。

.使用灰色显示图像。
image::customfilter-tinting.png[Tinting]

接下来我们想要解决滤镜的这个问题！
我们将在公式中包含offset。
(在经典Flash中，您可以使用 http://help.adobe.com/en_US/FlashPlatform/reference/actionscript/3/flash/geom/ColorTransform.html[ColorTransform].)

* 新红色值=（旧红色值×红色倍数）+ 红色偏移量
* 新绿色值=（旧绿色值×绿色倍数）+ 绿色偏移量
* 新蓝色值=（旧蓝色值×蓝色倍数）+ 蓝色偏移量
* 新透明值=（旧透明值×透明倍数）+ 透明偏移量

我们已经有了倍增器，因为它是在基本Mesh类中处理的;我们的滤镜只需要添加偏移量即可。

.向所有通道添加偏移量。
image::customfilter-offset.png[Offset]

现在进入正题,好吗?！ 

=== 扩展片段滤镜

所有的滤镜扩展自类'starling.filters.FragmentFilter`，这一个也不例外。
嘿，抓住了！我现在要扔给你一个完整的ColorOffsetFilter类;这不是代码片段，而是最终的完整代码！
我们不会再修改它。

[source, as3]
----
public class ColorOffsetFilter extends FragmentFilter
{
    public function ColorOffsetFilter(
        redOffset:Number=0, greenOffset:Number=0,
        blueOffset:Number=0, alphaOffset:Number=0):void
    {
        colorOffsetEffect.redOffset = redOffset;
        colorOffsetEffect.greenOffset = greenOffset;
        colorOffsetEffect.blueOffset = blueOffset;
        colorOffsetEffect.alphaOffset = alphaOffset;
    }

    override protected function createEffect():FilterEffect
    {
        return new ColorOffsetEffect();
    }

    private function get colorOffsetEffect():ColorOffsetEffect
    {
        return effect as ColorOffsetEffect;
    }

    public function get redOffset():Number
    {
        return colorOffsetEffect.redOffset;
    }

    public function set redOffset(value:Number):void
    {
        colorOffsetEffect.redOffset = value;
        setRequiresRedraw();
    }

    // the other offset properties need to be implemented accordingly.

    public function get/set greenOffset():Number;
    public function get/set blueOffset():Number;
    public function get/set alphaOffset():Number;
}
----

至目前为止，以上代码还是非常紧凑的，对吧？
但是我不得不承认：这个故事并没有讲完，因为我们也要写另一个类，来做实际的颜色处理工作。
无论如何，分析上面所看到的代码是非常值得的。

该类扩展自FragmentFilter，它覆盖了一个方法：`createEffect`。
以前你可能还没有遇到过`starling.rendering.Effect`类，因为只有低级渲染阶段才需要它。
通过阅读API文档可以得知：

====
Effect类封装了Stage3D绘图操作的所有步骤:
它配置渲染上下文，并设置着色器程序以及索引和顶点缓冲区，从而提供所有低级渲染的基本机制。
====

FragmentFilter类使用这个Effect类，或者Effect的子类FilterEffect。
对于这个简单的滤镜，我们只需要提供一个自定义的效果配置，我们通过重写“createEffect（）”方法来实现。
它只是通过属性来配置效果，并不做其它的工作。
在渲染时，基类将自动使用配置的效果来呈现滤镜。
仅此而已！

NOTE: 如果您想知道什么是“colorOffsetEffect”属性：它其实相当于一个快捷方式，能够访问配置的相关属性，而不会持续将其转换为ColorOffsetEffect。
基类也提供了一个`effect`属性，但它会返回一个类型为'FilterEffect`的对象 - 但我们需要完整的类型ColorOffsetEffect来访问“offset”属性。

更复杂的滤镜可能需要重写“process”方法;例如需要创建多通道滤镜。
但对于我们目前讨论的示例滤镜，不必重写此方法。

最后，请注意调用“setRequiresRedraw”：它确保在更改设置后重新呈现效果。
否则，Starling不会重绘对象。

=== 扩展FilterEffect

我们来做点实际的工作吧
FilterEffect子类是实现这个滤镜的重头戏。
这并不意味着它非常复杂，耐着性子听我讲完还是很容易理解的。

我们从一个尚未完成的代码开始：

[source, as3]
----
public class ColorOffsetEffect extends FilterEffect
{
    private var _offsets:Vector.<Number>;

    public function ColorOffsetEffect()
    {
        _offsets = new Vector.<Number>(4, true);
    }

    override protected function createProgram():Program
    {
        // TODO
    }

    override protected function beforeDraw(context:Context3D):void
    {
        // TODO
    }

    public function get redOffset():Number { return _offsets[0]; }
    public function set redOffset(value:Number):void { _offsets[0] = value; }

    public function get greenOffset():Number { return _offsets[1]; }
    public function set greenOffset(value:Number):void { _offsets[1] = value; }

    public function get blueOffset():Number { return _offsets[2]; }
    public function set blueOffset(value:Number):void { _offsets[2] = value; }

    public function get alphaOffset():Number { return _offsets[3]; }
    public function set alphaOffset(value:Number):void { _offsets[3] = value; }
}
----

请注意，我们将偏移量存储在Vector中，因为这样可以轻松将其上传到GPU。
`offset'属性从该向量读取并写入该向量。
就是这么简单。

当我们看看这两个重写的方法时，它会变得更有趣。

==== createProgram

该方法支持创建实际的Stage3D着色器代码。

[NOTE]
====
我将向您展示Stage3D的基础知识，但彻底解释Stage3D却超出了本手册的范围。
要想深入了解该主题，您可以随时查看以下教程：

  * http://www.adobe.com/devnet/flashplayer/articles/how-stage3d-works.html[Stage3D是如何工作的]
  * http://jacksondunstan.com/articles/1661[AGAL介绍]
  * http://help.adobe.com/en_US/as3/dev/WSd6a006f2eb1dc31e-310b95831324724ec56-8000.html[AGAL操作列表]
====

所有Stage3D渲染都是通过顶点着色器和片段着色器完成的。
这些是由GPU直接执行的小程序，它们有两种风格：

* *顶点着色器*对于每个顶点执行一次。
  他们的输入通常由“VertexData”类设置的顶点属性组成;它们的输出是屏幕坐标中顶点的位置。
* *片段着色器*对于每个像素（片段）执行一次。
  它们的输入由三角形的三个顶点的interpolated属性组成;输出便是具体像素的颜色。
* 一个片段着色器和一个顶点着色器共同组成一个*程序*。

着色语言由一种被成称为AGAL的汇编语言来编写。
（是的，你没看错！这是一种低级别的语言。）
幸运的是，典型的AGAL程序非常短，所以实际情况并不是像你想的那么糟糕。

有一个好消息：我们仅需要编写一个片段着色器。
因为顶点着色器相对于大多数片段着色器来说是相同的，所以Starling提供了一个标准的实现。
我们来看看代码：

[source, as3]
----
override protected function createProgram():Program
{
    var vertexShader:String = STD_VERTEX_SHADER;
    var fragmentShader:String =
        "tex ft0, v0, fs0 <2d, linear> \n" +
        "add oc, ft0, fc0";

    return Program.fromSource(vertexShader, fragmentShader);
}
----

如上所述事实并不难，顶点着色器取自常数;片段着色器只是两行代码。
两者组合成一个Program实例，并由一个方法返回。

当然，片段着色器还需要进一步阐述。

===== AGAL概述

在AGAL中，每行包含一个简单的方法调用。

  [操作码] [结果]，[参数 1]，（[可选参数 2]）

* 前三个字母是操作码的名称（例如`tex`，`add`）。
* 下一个参数定义了操作结果的保存位置。
* 其他参数是方法的实际参数。
* 所有数据存储在预定义的寄存器中;将它们视为Vector3D实例（具有x，y，z和w的属性）。

有几种不同类型的寄存器，例如用于常量，临时数据或着色器的输出。
在这些着色器中，其中一些已经包含数据;他们是通过滤镜的其他方法设置的（稍后会介绍）。

* `v0'包含当前纹理坐标（变量寄存器0）
* `fs0'指向输入纹理（纹理样本寄存器0）
* `fc0`包含这个颜色偏移量（片段常量寄存器0）

片段着色器的结果始终是一种颜色;该颜色将被存储在“oc”寄存器中。

===== 代码审查

让我们回到片段着色器的实际代码。
*第一行*从纹理读取颜色：

    tex ft0, v0, fs0 <2d, linear>

我们正在通过寄存器`v0'指定的纹理坐标和其它一些选项（`2d，linear`）来读取纹理`fs0`。
纹理坐标在“v0”中的原因只是因为标准顶点着色器（“STD_VERTEX_SHADER”）将它们存储在那里;在编写着色程序时记住这一点。
最后将结果存储在临时寄存器`ft0`（记住：在AGAL中，结果总是存储在操作码后的第一个参数中）。

[NOTE]
====
到目前为止我们还没有创建任何纹理，对吧？这是什么？

如上所述，片段着色器始终在像素级别工作;其输入是原始对象，输出结果呈现为纹理。
我们的基础类（FilterEffect）为我们设定了这个规则:当程序运行时，可以确保纹理采样器`fs0`将指向当前正在被过滤对象的像素。
====

你知道原因，其实我可能需要改写这一行。
您可能会注意到最后的可选参数，指定如何解释纹理数据。
事实证明如何编写这些可选参数取决于我们正在访问的纹理类型。
为了确保代码适用于每个纹理，我们统一使用一个帮助方法来写入该AGAL操作。

[source, as3]
----
tex("ft0", "v0", 0, this.texture)
----

实际结果是一样的（该方法返回一个AGAL字符串），但我们不再需要关心选项了。
始终使用此方法访问纹理;它会让你在晚上睡得更安稳。

*第二行*干的事情是这样的：它将颜色偏移添加到纹素颜色。
偏移量存储在`fc0`中，我们稍后会看看;这被添加到`ft0`寄存器（我们刚读取的纹素颜色）并存储在输出寄存器（'oc`）中。

    add oc, ft0, fc0

这就是AGAL现在做的事情。
我们来看看其他被重写的方法。

==== beforeDraw

`beforeDraw`方法在着色器执行之前执行。我们可以使用它们来设置我们的着色器所需的所有数据。

[source, as3]
----
override protected function beforeDraw(context:Context3D):void
{
    context.setProgramConstantsFromVector(Context3DProgramType.FRAGMENT, 0, _offsets);
    super.beforeDraw(context);
}
----

这是我们将偏移值传递给片段着色器的地方。
第二个参数“0”定义了数据将要结束的寄存器。
如果你回头看一下实际的着色器代码，你会看到我们读取'fc0'的偏移量，这正是我们在这里填补的：`fragment constant 0`。

超类方法完成所有其余的设置，例如分配纹理（`fs0`）和纹理坐标。

NOTE: 正如你想要询问的那样，确实还有一个`afterDraw（）`方法，通常用于清理资源。
但是对于常量来说，这并不是必需的，所以我们可以在这个滤镜中忽略它。

=== 尝试一下

我们的滤镜已经准备好了，实际上(下载完整的代码 https://gist.github.com/PrimaryFeather/ba1e26d568320cd31086[here])!
是时候跑一跑看看实际效果了.

[source, as3]
----
var image:Image = new Image(texture);
var filter:ColorOffsetFilter = new ColorOffsetFilter();
filter.redOffset = 0.5;
image.filter = filter;
addChild(image);
----

.我们的过滤器似乎有一个令人讨厌的副作用。
image::customfilter-pma.png[自定义过滤器PMA问题]

老天！
红色的值肯定是更高的，但为什么它现在超出了鸟的面积呢？
毕竟，我们并没有改变alpha值！

不要惊慌。
你刚刚创建了你的第一个滤镜，它并没有打击到你，对吧？
这一定是值得的。
只是需要一些微调。

事实证明，我们忘了考虑“前乘alpha”（PMA）。
所有常规纹理都以其alpha通道预先乘以RGB通道进行存储。
所以，一个红色与50％的alpha，像这样：

  R = 1, G = 0, B = 0, A = 0.5

实际上会像这样存储：

  R = 0.5, G = 0, B = 0, A = 0.5

我们没有考虑到这一点。
接下来需要需要做的是将偏移值与当前像素的alpha值相乘，然后再将其输出。
这是第一种方法：

[source, as3]
----
tex("ft0", "v0", 0, texture)   // 从纹理获取颜色
mov ft1, fc0                   // 将完整的偏移量复制到ft1
mul ft1.xyz, fc0.xyz, ft0.www  // 将颜色偏移量的rbg通道值分别乘以alpha（pma！）
add  oc, ft0, ft1              // 添加偏移量后将结果复制到输出寄存器
----

您可以看到，我们可以访问寄存器的“xyzw”属性来访问各个颜色通道（它们对应于我们的“rgba”通道）。

NOTE: 如果纹理不与PMA一起存储怎么办？
`tex`方法确保我们总是使用PMA接收值，所以不用担心。

==== 第二种尝试

当您再次尝试滤镜时(完整的代码： https://gist.github.com/PrimaryFeather/31f1dd7f04cd6ce886f1[ColorOffsetFilter.as]), 您会看到正确的Alpha值：

.这就对了！
image::customfilter-pma-solved.png[Custom Filter with solved PMA issue]

恭喜！
你刚刚完成了第一个滤镜，它工作的相当完美。
（是的，你可以刚刚使用Starling的“ColorMatrixFilter”，但是，这个比较快一点点，所以很值得一试。）

如果你内心坚定勇敢，那么现在可以尝试用网格样式来实现这个效果。
我承诺你的收获会大有不同！
