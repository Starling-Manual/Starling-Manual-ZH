== 多分辨率开发

嗯，我们在单一屏幕上开发游戏是什么时候的事了？
那时，我们在HTML页面中有一个小的矩形区域，那就是我们放置我们的精灵，文本和图像的地方。
一个单一的解决方案 - 不是很好吗？

唉...是改变的时候了！
手机具有各种类型和尺寸，甚至台式电脑和笔记本电脑都具有高密度显示器。
这对我们的消费者而言是一个好消息，但它并不能让我们这些开发者的开发生涯变得更容易，这是肯定的！

但不要放弃治疗：你可以搞定这个。
通过一系列需要预先考虑的事件，和利用Starling提供的一些简单的机制。

问题在于，万事开头难。
我们将在一个个小步骤中完成这个工作 - 我们将从2007年开始。

是的，你听到了：进入DeLorean，启动Flux Capacitor（TM），并按每小时八十英里的速度前进。

=== iPhone

iPhone可以说是休闲游戏最受欢迎的平台。
早在2007年，它也是你可以轻松开发的唯一一个平台。
这是大型App Store淘金热的时候！

其固定分辨率为320×480像素，第一款iPhone超级容易开发。
当然，Starling如果那时候就有了，你会这样启动它：

[source, as3]
----
var screenWidth:int  = stage.fullScreenWidth;
var screenHeight:int = stage.fullScreenHeight;
var viewPort:Rectangle = new Rectangle(0, 0, screenWidth, screenHeight);

starling = new Starling(Game, stage, viewPort);
----

我们将viewPort设置为屏幕的全尺寸：320×480像素。
默认情况下，舞台将具有完全相同的尺寸。

.我们的游戏运行在原始的iPhone中。
image::iphone-penguflip.png[PenguFlip on the iPhone, 120]

到目前为止，就这么简单：这就像在浏览器中的游戏一样。
(那会是Internet Explorer 6，对吧？)

下一站：2010。

=== iPhone视网膜屏幕

我们将DeLorean停在旧苹果校园的拐角处，查看App Store图表。
欢呼！显然，我们的游戏在2007年取得了巨大的成功，而且还在前十名！
没时间了，我们输不起：我们必须确保在几周内iPhone 4上看起来很好。

由于我们来自未来，我们知道其主要创新点：被苹果营销团队称为“视网膜显示”的高分辨率屏幕。
我们从2007年开始我们的游戏，并启动了这个尚待发布的设备。

.这绝对不是故意的。
image::iphone4-wrong.png[PenguFlip with a wrong scale on the iPhone4, 160]

可惜，游戏现在只占了四分之一的屏幕！
这是为什么？

如果你回顾我们在2007年写的代码，你会看到我们使viewPort和屏幕一样大。
使用iPhone 4，这些值翻了一番：其屏幕有640×960像素。
在舞台上放置显示对象的代码预计只有320×480的坐标系。
所以放在右边的东西（`x = 320`）现在突然在中心了。

这很容易解决。
记住：Starling的“viewPort”和“stageWidth / Height”属性可以独立设置。

* ViewPort(视口)决定了Starling显示的画面在屏幕的哪个区域。
  它总是以像素为单位指定。
* 舞台尺寸决定在该视口中显示的坐标系的大小。
  当您的舞台宽度为320时，任何x坐标在0和320之间的对象都将处于舞台中，无论viewPort的大小如何。

有了这个知识，升级之事就是小菜一碟：

[source, as3]
----
var screenWidth:int  = stage.fullScreenWidth;
var screenHeight:int = stage.fullScreenHeight;
var viewPort:Rectangle = new Rectangle(0, 0, screenWidth, screenHeight);

starling = new Starling(Game, stage, viewPort);
starling.stage.stageWidth  = 320;
starling.stage.stageHeight = 480;
----

viewPort仍然是动态的，具体取决于游戏开始的设备;但是，我们在底部添加了两行，将舞台大小硬编码为固定值。

NOTE: 由于这些值不再指示像素，我们现在称呼它们为：点：我们的舞台大小现在是320×480点。

在iPhone 4上，游戏现在看起来像这样：

.还行，有点模糊。
image::iphone4-scale-blurry.png[PenguFlip scaled up blurry, 285]

这更好：我们现在正在使用全屏尺寸。
但是，它也有点模糊。
我们并没有真正使用大屏幕。
我可以看到糟糕的评论进来了...我们需要解决这个问题！

==== 高清纹理

该问题的解决方案是提供高分辨率的特殊纹理。
根据像素密度，我们将使用低或高分辨率纹理集。
优点：除了选择纹理的逻辑，我们不需要更改我们的任何代码。

但是，仅仅加载一组不同的文件还不够。
毕竟，较大的纹理将为width和height返回更大的值。
我们固定的舞台宽度为320点。

* 宽度为160像素的SD纹理将填充舞台的一半;
* 相应的高清纹理（宽度：320像素）将填满整个舞台。

我们想要的是使HD纹理返回与SD纹理相同的大小，但提供更多的细节。

这时候,Starling的contentScaleFactor就派上用场了。
当我们配置Starling的stage和viewPort大小时，我们隐式地设置它。
使用上述设置，在iPhone 4上运行以下代码：

[source, as3]
----
trace(starling.contentScaleFactor); // → 2
----

contentScaleFactor返回viewPort width/stage width。
在视网膜设备上，将为“2”; 在非视网膜设备上，将为“1”。
这告诉我们在运行时加载哪些纹理。

NOTE: contentScaleFactor是一个整数不是巧合。
苹果公司每行/每列的像素数量翻倍，以尽可能避免混叠问题。

纹理类有一个类似的属性，简称为“scale”。
当正确设置时，纹理将按我们预想的那样工作。

[source, as3]
----
var scale:Number = starling.contentScaleFactor; // <1>
var texturePath:String = "textures/" + scale + "x"; // <2>
var appDir:File = File.applicationDirectory;

assetManager.scaleFactor = scale; // <3>
assetManager.enqueue(appDir.resolvePath(texturePath));
assetManager.loadQueue(...);

var texture:Texture = assetManager.getTexture("penguin"); // <4>
trace(texture.scale); // → Either '1' or '2' <5>
----
<1> 从Starling实例获取`contentScaleFactor`。
<2> 根据比例因子，纹理将从“1x”或“2x”目录加载。
<3> 通过将相同的比例因子分配给AssetManager，所有纹理将使用该值进行初始化。
<4> 访问纹理时，您不需要关注比例因子。
<5> 然而，您可以随时通过`scale`属性找出纹理的比例。

NOTE: 不使用AssetManager？
别担心，所有的“Texture.from ...”方法都包含一个额外的比例因子参数。
创建纹理时必须正确配置; 该值不能稍后更改。

当您查询纹理宽度或高度时，现在需要考虑scale factor。
例如，假如这是游戏全屏背景纹理,将会发生什么。

|===
|File |Size in Pixels |Scale Factor |Size in Points

|textures/1x/bg.jpg
|320×480
|1.0
|320×480

|textures/2x/bg.jpg
|640×960
|2.0
|320×480

|===

现在我们拥有所需的所有工具！

* 我们在后座的平面设计师（称他为Biff）以高分辨率创建所有纹理（理想情况下，作为矢量图形）。
* 在预处理步骤中，纹理将转换成我们要支持的实际分辨率（“1x”，“2x”）。
* 在运行时，我们检查Starling的contentScaleFactor并加载对应的纹理。

现在我们有一个清晰的视网膜游戏了！
我们的玩家将会喜欢它的，我确信这一点。

.现在我们正在使用视网膜屏幕！
image::iphone4-scale-hd.png[PenguFlip on the iPhone, 285]

TIP: 使用像 https://www.codeandweb.com/texturepacker/starling?source=gamua[TexturePacker] 这样的工具让事情变得非常简单.
给他们提供所有的纹理（最高分辨率），并让它们创建多个纹理图集，即每个缩放因子对应一个图集。

我们在Redwood的 http://gizmodo.com/5520438/how-apple-lost-the-next-iphone[bar] 上庆祝我们的成功，喝一两杯啤酒然后继续前进。

=== iPhone 5

2012年，iPhone对我们来说又有一个惊喜：苹果改变了屏幕的纵横比。
在水平方面，它仍然是640像素宽;但垂直方面，现在已经有点长了（1136像素）。
它仍然是一个视网膜屏幕，当然，我们的新的逻辑分辨率变成了320×568点。

作为一个快速的解决方案，我们只需将我们的舞台放在viewPort上，并且让它伴随顶部和底部的黑色条纹一起呈现。

[source, as3]
----
var offsetY:int = (1136 - 960) / 2;
var viewPort:Rectangle = new Rectangle(0, offsetY, 640, 960);
----

嗯, 似乎正常工作了！
对于在这个时间线上开始出现的所有Android智能手机来说，这是一个公平的策略。
是的，我们的游戏在某些设备上可能看起来有点模糊，但这并不太糟糕：图像质量仍然令人惊喜。
大多数用户不会注意到。

.宽幕缩放.
image::iphone5-letterbox.png[PenguFlip with letterbox bars, 160]

我称之为*黑边模式*。

* 以固定的舞台大小开发游戏（如320×480点）。
* 根据比例因子（例如“1x”，“2x”，“3x”）添加几组资源。
* 然后，您可以扩展应用程序，使其填充屏幕而不会发生任何失真。

这可能是最务实的解决方案。
它允许您的游戏在所有可用的显示分辨率上以可接受的质量运行，除了将viewPort设置为正确的大小外，您不必进行任何额外的工作。

顺便说一句，当您使用Starling附带的RectangleUtil时，事情变得非常容易处理。
要“放大”您的viewPort，只需使用以下代码创建它：

[source, as3]
----
const stageWidth:int  = 320; // points
const stageHeight:int = 480;
const screenWidth:int  = stage.fullScreenWidth; // pixels
const screenHeight:int = stage.fullScreenHeight;

var viewPort:Rectangle = RectangleUtil.fit(
    new Rectangle(0, 0, stageWidth, stageHeight),
    new Rectangle(0, 0, screenWidth, screenHeight),
    ScaleMode.SHOW_ALL);
----

简单高效！
我们乘坐时光机再来一次旅行。
出发！

=== iPhone 6 and Android

我们现在在2014年...天哪！
检查“苹果商店年鉴”，我们发现在上次更新后，我们的销售情况并不好。
显然，苹果对我们的黑边方法并不满意，这次并没有提供这个功能给我们。
可恶。

嗯，我想我们现在别无选择了，让我们咬住子弹，并利用这个额外的屏幕空间。
是告别硬编码坐标的时候了，我们竟然用了这么久！
从现在开始，我们需要为所有的显示对象使用相对位置。

我会称这个策略*智能对象布局*。
启动代码仍然非常相似：

[source, as3]
----
var viewPort:Rectangle = new Rectangle(0, 0, screenWidth, screenHeight);

starling = new Starling(Game, stage, viewPort);
starling.stage.stageWidth  = 320;
starling.stage.stageHeight = isIPhone5() ? 568 : 480;
----

是的，我也感觉到哪里不对。
根据我们正在运行的设备硬编码舞台高度...这不是一个非常聪明的想法。
我承诺，我们即将解决这个问题。

到现在为止，它能正常的工作，毕竟：viewPort和stage都有正确的大小。
但是我们该怎么做显得更聪明呢？
我们现在来看看Game类，这个类将作为Starling的根类。

[source, as3]
----
public class Game extends Sprite
{
    public function Game()
    {
        addEventListener(Event.ADDED_TO_STAGE, onAddedToStage); // <1>
    }

    private function onAddedToStage():void
    {
        setup(stage.stageWidth, stage.stageHeight); // <2>
    }

    private function setup(width:Number, height:Number):void
    {
        // ...

        var lifeBar:LifeBar = new LifeBar(width); // <3>
        lifeBar.y = height - lifeBar.height;
        addChild(lifeBar);

        // ...
    }
}
----
<1> 当游戏的构造函数被调用时，它尚未连接到舞台。所以我们推迟初始化，直到连接到舞台。
<2> 我们调用自定义的“setup”方法，并传入舞台大小。
<3> 示例，我们在屏幕底部创建一个LifeBar实例（一个自定义的用户界面类）。

总而言之，这不是太难，对吧？
诀窍是始终考虑舞台尺寸。
在这里，如果您在干净的组件中创建游戏，并且单独的类负责不同的界面元素，这将是有益的。
对于任何有意义的元素，您可以传递大小（如上面的LifeBar构造函数），并使其相应地执行。

.没有更多的黑边吧：使用上完整的屏幕。
image::iphone5-smart-objects.png[PenguFlip without letterbox bars, 160]

这在iPhone 5上非常出色。
我们应该在2012年做到这一点，而不是2014年！
在2014年，事情变得更复杂了。

* Android正在迅速获得市场份额，手机具有不同的尺寸和分辨率。
* 苹果公司也推出了更大的屏幕 - iPhone 6和iPhone 6 Plus。
* 我是否提到平板电脑？

我们通过组织相对于舞台尺寸的显示对象，已经奠定了解决这个问题的基础。
我们的游戏几乎能运行任何尺寸的舞台之上。

剩下的问题是使用一个值-舞台大小和内容的比例因子。
看看我们必须处理的屏幕区域，这似乎是一项艰巨的任务！

|===
|Device |Screen Size |Screen Density |Resolution

|iPhone 3
|3,50"
|163 dpi
|320×480

|iPhone 4
|3,50"
|326 dpi
|640×960

|iPhone 5
|4,00"
|326 dpi
|640×1136

|iPhone 6
|4,70"
|326 dpi
|750×1334

|iPhone 6 Plus
|5,50"
|401 dpi
|1080×1920

|Galaxy S1
|4,00"
|233 dpi
|480×800

|Galaxy S3
|4,80"
|306 dpi
|720×1280

|Galaxy S5
|5,10"
|432 dpi
|1080×1920

|Galaxy S7
|5,10"
|577 dpi
|1440×2560

|===

确定比例因子的关键是考虑到屏幕的密度。

* 密度越高，比例因子越高。
  换句话说：我们可以从密度推断出比例因子。
* 从比例因子我们可以计算适当的舞台大小。
  基本上我们扭转了我们以前的做法。

原始iPhone的屏幕密度约为160 dpi。
我们将其作为我们计算的基础：对于任何设备，我们将密度除以160，并将结果取为大于或等于当前值的最近一个整数。
下面对这种做法进行细致的检查。

|===
|Device |Screen Size |Screen Density |Scale Factor |Stage Size

|iPhone 3
|3,50"
|163 dpi
|1.0
|320×480

|iPhone 4
|3,50"
|326 dpi
|2.0
|320×480

|iPhone 5
|4,00"
|326 dpi
|2.0
|320×568

|iPhone 6
|4,70"
|326 dpi
|2.0
|375×667

|iPhone 6 Plus
|5,50"
|401 dpi
|3.0
|414×736

|Galaxy S1
|4,00"
|233 dpi
|1.5
|320×533

|Galaxy S3
|4,80"
|306 dpi
|2.0
|360×640

|Galaxy S5
|5,10"
|432 dpi
|3.0
|360×640

|Galaxy S7
|5,10"
|577 dpi
|4.0
|360×640

|===

看看计算出来的舞台的大小：现在它们从320×480到414×736点。
这是一个适度的范围，这也是有道理的：一个物理尺寸更大的屏幕料想会有更大的舞台。
重要的是，通过选择适当的比例因子，我们得到了合理的坐标系。
这是我们可以依据的尺寸范围！

NOTE: 您可能已经注意到Galaxy S1的比例因子不是整数值。
这是必要的，以达到可接受的舞台大小。

我们来看看我是如何得到这些比例因子的。
创建一个名为“ScreenSetup”的类，并从以下内容开始：

[source, as3]
----
public class ScreenSetup
{
    private var _stageWidth:Number;
    private var _stageHeight:Number;
    private var _viewPort:Rectangle;
    private var _scale:Number;
    private var _assetScale:Number;

    public function ScreenSetup(
        fullScreenWidth:uint, fullScreenHeight:uint,
        assetScales:Array=null, screenDPI:Number=-1)
    {
        // ...
    }

    public function get stageWidth():Number { return _stageWidth; }
    public function get stageHeight():Number { return _stageHeight; }
    public function get viewPort():Rectangle { return _viewPort; }
    public function get scale():Number { return _scale; }
    public function get assetScale():Number { return _assetScale; }
}
----

这个类将会找出Starling如何合理配置viewPort和stage size。
大多数属性应该是其意自见的 - 或许`assetScale`除外。

上表显示，我们将以“1”到“4”的比例因子结束。
但是，我们可能不想为所有尺寸都创建匹配的纹理。
反正最密集的屏幕的像素很小，你的眼睛不可能区分它们。
因此，您通常只会为这些比例因子的一部分（例如1-2或1-3）提供资源。

* 构造函数中的`assetScales`参数应该是一个填充了您创建纹理的比例因子的数组。
* “assetScale”属性将告诉您需要加载哪些资源图集。

TIP: 如今，应用程序要求比例因子“1”的情况已经是罕见的。
但是，在开发过程中，该尺寸非常方便，因为您可以预览界面而不需要极大的电脑屏幕。

我们来看看那个构造函数的实现。

[source, as3]
----
public function ScreenSetup(
    fullScreenWidth:uint, fullScreenHeight:uint,
    assetScales:Array=null, screenDPI:Number=-1)
{
    if (screenDPI <= 0) screenDPI = Capabilities.screenDPI;
    if (assetScales == null || assetScales.length == 0) assetScales = [1];

    var iPad:Boolean = Capabilities.os.indexOf("iPad") != -1; // <1>
    var baseDPI:Number = iPad ? 130 : 160; // <2>
    var exactScale:Number = screenDPI / baseDPI;

    if (exactScale < 1.25) _scale = 1.0; // <3>
    else if (exactScale < 1.75) _scale = 1.5;
    else _scale = Math.round(exactScale);

    _stageWidth  = int(fullScreenWidth  / _scale); // <4>
    _stageHeight = int(fullScreenHeight / _scale);

    assetScales.sort(Array.NUMERIC | Array.DESCENDING);
    _assetScale = assetScales[0];

    for (var i:int=0; i<assetScales.length; ++i) // <5>
        if (assetScales[i] >= _scale) _assetScale = assetScales[i];

    _viewPort = new Rectangle(0, 0, _stageWidth * _scale, _stageHeight * _scale);
}
----
<1> 我们需要为Apple iPad添加一个小的解决方法。 我们希望它使用与iOS上本机相同的比例因子。
<2> 我们的基本密度为160 dpi（iPad上为130 dpi）。 具有这种密度的设备将使用比例因子“1”。
<3> 我们的比例因子应该是整数值或“1.5”。 该代码选择最接近的一个。
<4> 在这里，我们选择要加载的资源集。

TIP: 如果要查看此代码在上表设备上运行的结果，请参阅此 https://gist.github.com/PrimaryFeather/505fc4f120c06d09d2c56910476ab710[Gist].
您可以轻松地在此列表中添加更多设备，并查看您是否对结果感到满意。

现在，一切都到位，我们可以调整Starling的启动代码。
此代码假定您提供的比例因子为“1”和“2”。

[source, as3]
----
var screen:ScreenSetup = new ScreenSetup(
    stage.fullScreenWidth, stage.fullScreenHeight, [1, 2]);

_starling = new Starling(Root, stage, screen.viewPort);
_starling.stage.stageWidth  = screen.stageWidth;
_starling.stage.stageHeight = screen.stageHeight;
----

在加载资源时，使用`assetScale`属性。

[source, as3]
----
var scale:Number = screen.assetScale;
var texturePath:String = "textures/" + scale + "x";
var appDir:File = File.applicationDirectory;

assetManager.scaleFactor = scale;
assetManager.enqueue(appDir.resolvePath(texturePath));
assetManager.loadQueue(...);
----

就是这样!
您仍然需要时刻考虑舞台大小以设置您的用户界面与之相适应，但这绝对是可以做到的。

TIP: Starling存储库包含一个名为Mobile Scaffold的项目，其中包含所有这些代码。
这是任何移动应用程序的完美起点。
(如果您在下载中找不到ScreenSetup类，请查看GitHub项目的最新版本)

TIP: 如果您使用Feathers，类ScreenDensityScaleFactorManager将执行我们上面写的ScreenSetup类所做的工作。
事实上，这里描述的逻辑很多的灵感来自于该类。

=== iPad和其他平板电脑

回过头来看, 我们开始怀疑将游戏移植到平板电脑是否有意义。
上面的代码在平板电脑上工作得很好，然而我们将面临更大的舞台，更多的内容空间。
如何处理这取决于您正在创建的应用程序。

==== 有些游戏可以简单地放大。

像Super Mario Bros或Bejeweled这样的游戏看起来可以很大地缩放到具有详细纹理的大屏幕。
在这种情况下，您可以忽略屏幕密度，并根据可用像素的数量计算缩放因子。

* 第一款iPad（分辨率：768×1024）将简单地成为舞台大小为384×512，比例因子为“2”的适配设备。
* 视网膜iPad（分辨率：1536×2048）的舞台大小也是384×512，比例因子为“4”。

==== 显示更多的内容。

想想Sim City或Command＆Conquer：这样的游戏可以向用户展示更多的景观。
与游戏的内容相比，用户界面元素将占用更少的空间。

==== 有些需要你重新思考完整的用户界面。

对于app尤其如此。
在手机的小屏幕上，电子邮件客户端将单个显示邮件，收件箱或邮箱。
但是，平板电脑可以一次显示这三个元素。
不要低估这将导致增加的开发工作量。
