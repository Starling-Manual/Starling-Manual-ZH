== 配置Starling

每个Starling应用程序的第一步：创建一个Starling类的实例（包名starling.core）.
下面是Starling的构造函数的完整声明:

[source, as3]
----
public function Starling(
    rootClass:Class,
    stage:Stage,
    viewPort:Rectangle = null,
    stage3D:Stage3D = null,
    renderMode:String = auto,
    profile:Object = auto);
----

rootClass:: 一旦Stage3D完成初始化，必须实例化的类。 根类是以下类的一个子类 `starling.display.DisplayObject`.

stage:: 传统的Flash通过此类承接Starling的内容。 这就是Starling和Flash显示列表相互连接的关键所在.

viewPort:: 定义Starling渲染的一个区域(Flash Stage中)。由于这通常是完整的大小，您可以忽略此参数（即传递null）以使用完整区域.

stage3D:: 用于渲染的Stage3D实例。每个Flash阶段可能包含多个Stage3D实例，您可以选择其中任何一个。然而，默认参数（null）将足以满足大多数需求.

renderMode:: Stage3D背后的整体理念是提供硬件加速渲染。但是，还有一种软件回退模式;它可能会通过传递Context3DRenderMode.SOFTWARE来强制。建议使用默认（自动），但这意味着软件渲染仅在没有其他选项时使用.

profile:: Stage3D提供了一组分组到不同配置文件（定义为Context3DProfile中的常量）的功能。应用程序运行的硬件越好，可用配置文件越好。默认（自动）是选择最好的可用配置文件.

大多数这些参数具有正确的默认值，因此您可能不需要指定所有这些值。下面的代码显示了启动Starling的最直接的方法。这是Flash Player或AIR项目的Main Class.

[source, as3]
----
package
{
    import flash.display.Sprite;
    import starling.core.Starling;

    [SWF(width="640", height="480",
         backgroundColor="#808080",
         frameRate="60")]
    public class Main extends Sprite
    {
        private var _starling:Starling;

        public function Main()
        {
            _starling = new Starling(Game, stage);
            _starling.start();
        }
    }
}
----

注意类扩展自flash.display.Sprite，而不是Starling变量.
这是AS3中所有Main类的必要条件.
然而，一旦Starling完成启动，逻辑就转移到Game类，它构建了我们与starling.display世界的链接.

[NOTE]
.配置帧速率
====
有些设置位于类前面的“SWF”MetaData标签中.
帧速率是其中之一.
Starling没有类似设置：它总是简单地使用flash本身的frameRate.
在运行时更改它，请访问nativeStage属性:

[source, as3]
----
Starling.current.nativeStage.frameRate = 60;
----
====

Starling的设置过程是异步的.
这意味着您将无法在Main方法结束时访问Game实例.
但是，可以侦听ROOT_CREATED事件，以在类实例化完成后收到通知.

[source, as3]
----
public function Main()
{
    _starling = new Starling(Game, stage);
    _starling.addEventListener(Event.ROOT_CREATED, onRootCreated);
    _starling.start();
}

private function onRootCreated(event:Event, root:Game):void
{
    root.start(); // 'start' needs to be defined in the 'Game' class
}
----

=== The ViewPort

Stage3D为Starling提供了一个矩形区域.
该区域可以在Flash舞台的任何位置：Flash Player或应用程序（如果是AIR项目）窗口区域内的任何位置.

在Starling中，该区域称为viewPort.
大多数时候，你使用区域将是整个Flash舞台的尺寸，但有时，限制渲染到某个局部区域是有意义的.

想象一个设计的长宽比为4：3的游戏，运行在16：9的屏幕上.
通过在屏幕上居中4：3 viewPort，你会得到一个“letterboxed”游戏，在顶部和底部有黑色条.

// TODO: add image

离开Starling的stage谈论viewPort是没有意义的.
默认情况下，stage尺寸将与viewPort的大小完全相同.
这是很有道理的：一个显示器大小为1024×768像素的设备应该有一个相同大小的舞台.

你可以自定义舞台大小.
这可以通过stage.stageWidth和stage.stageHeight属性来设置:

[source, as3]
----
stage.stageWidth = 1024;
stage.stageHeight = 768;
----

这是什么意思?
绘图区域的大小现在是由viewPort还是stage大小定义的?

不要担心，该区域仍然只由viewPort设置，如上所述.
修改stageWidth和stageHeight不会改变绘图区域的大小;
舞台总是会覆盖整个viewPort.
你改变stageWidth和stageHeight,实际上改变的只是舞台坐标系的大小.

这意味着，当舞台宽度为1024时，x坐标为1000的对象将接近舞台的右边缘;无论viewPort的宽度值是512，1024，还是2048像素.

这在开发HiDPI屏幕上的应用程序时特别有用.
例如，苹果的iPad存在于普通和“视网膜”显示屏版本，后者像素行和列的数量翻倍（产生四倍的像素总量）.
在这种屏幕上，界面元素不应变得更小;相反，它们应该更清晰.

通过举例来区分viewPort和舞台大小，这种例子很容易在Starling中出现.
在以上两种设备类型上，设备显示屏大小相同，其舞台大小都是1024×768;但是，viewPort将以像素为单位反映屏幕的分辨率.
优点：您可以在显示屏尺寸相同的设备上为显示对象使用相同的坐标，而不管运行应用程序的设备配置了哪种显示屏.

[NOTE]
.点 vs. 像素
====
如果你理解这一点，你会看到在这样的视网膜设备上，x坐标为1的对象实际上离原点两个像素.
换句话说，用于测量的单位已经改变.
我们不再谈论'像素'，而是谈论'点'!
在低分辨率屏幕上，一点等于一个像素; 在HiDPI屏幕上，一点等于两个像素（或更多，取决于设备）.
====

要找出点的实际宽度（以像素为单位），您可以简单地将`viewPort.width`除以`stage.stageWidth`.
或者你使用Starling的`contentScaleFactor`属性，就像这样:

[source, as3]
----
starling.viewPort.width = 2048;
starling.stage.stageWidth = 1024;
trace(starling.contentScaleFactor); // -> 2.0
----

在“移动开发”一章,我会告诉你如何充分利用这个概念.

=== Context3D Profiles

Starling运行的平台具有各种图形处理器.
当然，那些图形处理器(GPU)具有不同的能力.
问题是：如何在运行时区分这些能力?

这就是Context3D profiles（也称为render profiles）的用途.

[NOTE]
.什么是Context3D?
====
当使用Stage3D时，您不可避免的将与渲染管道打交道，而渲染管道具有许多设置和属性.
context(设置上下文)就是封装这些管道的对象.
创建纹理，上传着色器，渲染三角形 - 这一切都是通过context完成的.
====

实际上，Starling尽力隐藏任何配置文件限制.
为了确保最广泛的覆盖范围，它被设计成默认尝试使用最低可用配置文件.
同时，当运行在含有更高的配置文件环境时，它会自动适配最佳的配置文件.

然而，了解它们的基本特征终归是有用的.
以下是每个配置文件的概述，从最低的开始.

`BASELINE_CONSTRAINED`:: 如果设备完全支持Stage3D，它必须支持此配置文件。它有几个限制，例如:它只支持边长为2的幂的纹理，并且着色器的长度非常有限。该配置文件主要出现在旧台式计算机上.

`BASELINE`:: 在移动设备上出现的最低配置文件。 Starling在此配置文件下运行良好;去除了纹理边长必须是2的幂限制;允许更有效率的使用存储器，并且着色器程序的长度往往满足其需求.

`BASLINE_EXTENDED`:: 将最大纹理尺寸从“2048x2048”提高到“4096x4096”像素，这对高分辨率设备至关重要.

`STANDARD_CONSTRAINED`, `STANDARD`, `STANDARD_EXTENDED`:: Starling当前不需要使用这些配置文件中的任何功能。它们提供额外的着色器命令和其他低级控制能力.

我的建议：让Starling自动选择最好的可用配置文件（`auto`），让它自行处理.

[NOTE]
.最大纹理尺寸
====
有一件事你自己必须小心：确保你的纹理不是太大.
最大纹理尺寸可以通过属性“Texture.maxSize”访问，前提是在Starling已经完成初始化之后.
====

=== 原生叠加

Starling背后的主要目标是使用Stage3D驱动的API来加速渲染.
然而，不可否认的是：经典的显示列表有许多功能，Starling无法提供.
因此，提供一个简单的方法来综合Starling和经典Flash的功能是有意义的.

`nativeOverlay`属性是最简单的方法.
这是一个常规的“flash.display.Sprite”，它直接位于Starling层的顶部，使用时请将viewPort和contentScaleFactor因素加以考虑.
如果您需要使用原生Flash对象，请将它们添加到此叠加层.

但要注意，在Stage3D之上的原生Flash内容可能导致某些（移动）平台的性能损失。 因此，当您不再需要它们时，始终从叠加层中删除所有对象.

[NOTE]
====
假如你有疑问：不，你不能添加任何原生显示对象到Starling显示对象之下.
是的,Stage3D表面总是位于flash底部; 没有办法越过这道槛.
====

=== 跳过不变帧

在应用程序或游戏中经常出现几个帧完全相同的情形.
例如，应用可能正在呈现静态画面或等待用户输入.
那么有必要在这些情况下重新绘制舞台吗?

这正是'skipUnchangedFrames`属性的要点所在.
如果启用，静态画面会被自动识别，以便后台缓冲区保留原样.
在移动设备上，此性能的提升虽不能被高估.
但没有比这更好的方法来提高电池寿命了!

我已经听到你的反对意见：如果这个功能是如此有用，为什么不是默认激活?
这里有一个坑，对吧?

确实有：它不能很好地与Render和VideoTextures同时工作.
这些纹理的变化根本不会被呈现.
不过这很容易解决：暂时禁用`skipUnchangedFrames`，或每当它们的内容有改变时调用`stage.setRequiresRedraw（）`.

现在你知道了这个功能，那么总是激活它，使它成为一个习惯吧!
在此期间，我希望我可以在未来的Starling版本中解决上述问题.

IMPORTANT: 在移动平台上，您还应注意另一个限制：只要本机（Flash）舞台上有任何原生显示对象（例如通过Starling的“nativeOverlay”添加的），Starling就不能跳过任何帧.
这是Stage3D限制的结果.

=== 统计显示

在开发应用程序时，您需要尽可能多的信息来了解发生了什么.
这样，你能够早点发现问题，并可能由此避免进入死胡同.
统计显示就是用来干这件事的.

[source, as3]
----
_starling.showStats = true;
----

.统计显示 (默认在左上角).
image::stats-display.png[The statistics display]

这些值的含义是什么?

* framerate (帧率)意义应该是相当明确的: Starling统计到在前一秒内渲染的帧数.
* Standard memory 是内存, 总而言之, 你使用的 AS3 对象, 无论它是 String,  Sprite,  Bitmap, 或者 Function: 所有对象都需要一些内存.
  单位是兆字节(Mb).
* GPU memory 表示显存，这分几种情况. 纹理存储在图形存储器（显存）中，顶点缓冲和着色器程序也是如此.
  大多数时候，纹理占据了绝大部分.
* draw calls 表示绘制调用次数,即每帧向GPU发送多少个单独的“绘制”命令.
  通常，当绘制调用较少时，场景渲染会更快.
  当我们谈论<<性能优化>>时，我们将详细解读这个值.

您可能会注意到统计显示的背景颜色在黑色和深绿色之间交替.
这是一个微妙的线索，指的是`skipUnchangedFrames`属性:
当大部分帧可以跳过时，该框变为绿色.
每当画面静止时，确保它保持绿色; 如果没有, 一些逻辑导致了Starling不能进入跳帧操作.

TIP: 您可以通过`showStatsAt`方法在屏幕上自定义统计显示的位置.
