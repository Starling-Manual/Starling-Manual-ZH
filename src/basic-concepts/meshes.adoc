== 网格

Mesh是所有有形显示对象的基本构建块。
“有形”是指显示列表中的一个“叶”对象：一个不是容器但直接呈现给后台缓冲区的对象。
因为它是如此重要，我想更详细地谈谈这个类。

简而言之，Mesh表示通过Stage3D渲染的三角形列表。
它已经被提到了几次，因为它是Quad和Image的基类。
作为提醒，先列出我们正在谈论的类层次结构：

[plantuml, mesh-classes-from-display-object]
....
include::../defaults.plantuml[]

DisplayObject <|-- Mesh
Mesh <|-- Quad
Quad <|-- Image
....

Mesh不是一个抽象类; 没有什么阻止你直接实例化它。
就是这样：

[source, as3]
----
var vertexData:VertexData = new VertexData();
vertexData.setPoint(0, "position", 0, 0);
vertexData.setPoint(1, "position", 10, 0);
vertexData.setPoint(2, "position", 0, 10);

var indexData:IndexData = new IndexData();
indexData.addTriangle(0, 1, 2);

var mesh:Mesh = new Mesh(vertexData, indexData);
addChild(mesh);
----

正如你所看到的，我们首先需要实例化两个类：VertexData和IndexData。
它们分别表示顶点和索引的集合。

* VertexData有效地存储每个顶点的属性，例如。其位置和颜色。
* IndexData存储这些顶点的索引。每三个指数将构成一个三角形。

这样，上面的代码创建了最基本的绘图单元：三角形。
我们通过定义三个顶点并顺时针引用它们来做到这一点。
毕竟，GPU非常擅长做这件事：绘制三角形 - 非常多的三角形。

.我们刚刚创建的三角形。
image::triangle.png[Triangle, 180]

=== 扩展网格

直接使用VertexData和IndexData将是相当麻烦的。
将所需要的设置代码封装在一个类中是有意义的。

为了说明如何创建自定义网格，我们现在将编写一个名为NGon的简单类。
它的任务：用自定义颜色渲染常规的n边形。

.这些是我们要绘制的普通多边形的种类。
image::polygons.png[Polygons]

我们希望此类用起来像一个flash内置的显示对象。
您实例化它，将其移动到某个位置，然后将其添加到显示列表。

[source, as3]
----
var ngon:NGon = new NGon(100, 5, Color.RED); // <1>
ngon.x = 60;
ngon.y = 60;
addChild(ngon);
----
<1> 构造函数参数定义半径，边数和颜色。

让我们看看我们如何能够实现这个功绩。

=== 顶点设置

像所有其他形状，我们的正多边形也是由几个三角形构成。
下面是我们如何设置一个五边形的三角形（一个n边多边形，n = 5）。

.五边形及其顶点。
image::pentagon.png[Pentagon, 200]

五边形由六个顶点构成，横跨五个三角形。
我们给每个顶点一个在0和5之间的数字，5在中心。

如上所述，顶点存储在VertexData实例中。
VertexData为每个顶点定义一组属性。
在这个示例中，我们需要两个标准属性：

* `position`存储一个二维点（x，y）。
* `color`存储RGBA颜色值。

VertexData类定义了一些访问这些属性的方法。
这允许我们设置多边形的顶点。

创建一个名为NGon的新类，​​扩展自Mesh。
然后添加以下实例方法：

[source, as3]
----
private function createVertexData(
    radius:Number, numEdges:int, color:uint):VertexData
{
    var vertexData:VertexData = new VertexData();

    vertexData.setPoint(numEdges, "position", 0.0, 0.0); // <1>
    vertexData.setColor(numEdges, "color", color);

    for (var i:int=0; i<numEdges; ++i) // <2>
    {
        var edge:Point = Point.polar(radius, i*2*Math.PI / numEdges);
        vertexData.setPoint(i, "position", edge.x, edge.y);
        vertexData.setColor(i, "color", color);
    }

    return vertexData;
}
----
<1> 设置中心顶点（最后一个索引）。
<2> 设置边顶点。

由于我们的网格有一个统一的颜色，我们为每个顶点分配相同的颜色。
边缘顶点（拐角）的位置沿着具有给定半径的圆分布。

=== 索引设置

这么多的顶点。
现在，我们需要定义组成多边形的三角形。

Stage3D想要一个简单的列表，即索引列表，每三个连续的索引组成一个三角形。
顺时针参考指标是一个好的做法; 这个约定表示我们正在看三角形的前侧。
我们的五边形的列表将如下所示：

    5, 0, 1,   5, 1, 2,   5, 2, 3,   5, 3, 4,   5, 4, 0

在Starling中，IndexData类用于设置索引列表。
以下方法将使用适当的索引填充IndexData实例。

[source, as3]
----
private function createIndexData(numEdges:int):IndexData
{
    var indexData:IndexData = new IndexData();

    for (var i:int=0; i<numEdges; ++i)
        indexData.addTriangle(numEdges, i, (i+1) % numEdges);

    return indexData;
}
----

=== NGon构造函数

这实际上是我们需要的NGon类！
现在我们只需要在构造函数中使用上面的方法。
显示对象的所有其他职责（碰撞测试，渲染，边界计算等）由超类处理。

[source, as3]
----
public class NGon extends Mesh
{
    public function NGon(
        radius:Number, numEdges:int, color:uint=0xffffff)
    {
        var vertexData:VertexData = createVertexData(radius, numEdges, color);
        var indexData:IndexData = createIndexData(numEdges);

        super(vertexData, indexData);
    }

    // ...
}
----

这很直接，不是吗？
这种方法适用于你可以想到的任何形状。

[TIP]
====
当使用自定义网格时，还要查看Polygon类（在`starling.geom`包中）。
它有助于将任意封闭形状（由多个顶点定义）转换为三角形。
我们在<< 遮罩 >>部分更详细地讨论它。
====

=== 添加纹理

如果我们能够将纹理映射到这个多边形，不是很好吗？
基类Mesh已经定义了一个`texture`属性;我们只缺少所需的纹理坐标。

通过纹理坐标，您可以定义纹理的哪个部分映射到哪个顶点。
它们通常称为UV坐标，它是对通常用于其坐标轴（u和v）的名称的引用。
请注意，UV范围定义在0和1内，是一个比值，与实际纹理尺寸无关。

.多边形的纹理坐标在0-1的范围内。
image::pentagon-texcoords.png[Pentagon Texture Coordinates, 250]

有了这些信息，我们可以相应地更新`createVertexData`方法。

[source, as3]
----
function createVertexData(
    radius:Number, numEdges:int, color:uint):VertexData
{
    var vertexData:VertexData = new VertexData(null, numEdges + 1);
    vertexData.setPoint(numEdges, "position", 0.0, 0.0);
    vertexData.setColor(numEdges, "color", color);
    vertexData.setPoint(numEdges, "texCoords", 0.5, 0.5); // <1>

    for (var i:int=0; i<numEdges; ++i)
    {
        var edge:Point = Point.polar(radius, i*2*Math.PI / numEdges);
        vertexData.setPoint(i, "position", edge.x, edge.y);
        vertexData.setColor(i, "color", color);

        var u:Number = (edge.x + radius) / (2 * radius); // <2>
        var v:Number = (edge.y + radius) / (2 * radius);
        vertexData.setPoint(i, "texCoords", u, v);
    }

    return vertexData;
}
----
<1> 中心顶点的纹理坐标：`0.5，0.5`。
<2> 正多边形的原点在中心，但纹理坐标必须都为正。
    因此，我们将顶点坐标向右移动（通过`radius`），并将它们除以`2 * radius`，结束在范围`0-1`内。

当分配纹理时，渲染代码将自动选取这些值。

[source, as3]
----
var ngon:NGon = new NGon(100, 5);
ngon.texture = assets.getTexture("brick-wall");
addChild(ngon);
----

.我们的纹理五边形。
image::pentagon-textured.png[Textured Pentagon, 230]

=== 抗锯齿

如果你仔细观察我们的正多边形的边缘，你会看到边缘锯齿相当严重。
这是因为GPU处理正多边形内部或外部的像素时 - 并没有平滑像素化边缘。
要解决这个问题，您可以启用反锯齿：在Starling类中有一个具有该名称的属性。

[source, as3]
----
starling.antiAliasing = 4;
----

该值与Stage3D在渲染时使用的子样本数相关。
使用更多的子样本数需要执行更多的计算，使得抗锯齿可能成为非常昂贵的选项。
此外，Stage3D并非在所有平台上的都支持抗锯齿。

NOTE: 在移动设备上，目前抗锯齿仅在RenderTextures中有效。

因此，它不是一个理想的解决方案。
我可以提供的唯一的好消息：屏幕的像素密度不断上升。
在现代高端手机上，像素如此之小，以致于锯齿很少成为一个问题。

.抗锯齿可以平滑像素化边缘。
image::anti-aliasing.png[Anti-Aliasing, 380]

// TODO: 在“Pro-Tips”章节中描述渲染纹理解决方案，并链接到这里。

=== 网格样式

你现在知道该如何创建任意形状的纹理网格了。
如果仅仅如此，您使用Starling内置的标准渲染机制就可以了。

然而，如果你想自定义渲染过程呢？
虽然Mesh类的属性和方法提供了坚实的基础 - 但迟早，你会觉得这些还不够。

Starling的网格样式可以解决这种问题。

样式是Starling的新增内容（在2.0版中引入），是创建自定义高性能渲染代码的推荐方式。
事实上，Starling中的所有渲染都是通过网格样式完成的。

* 样式可以分配给任何网格物体（Mesh类或其子类的实例）。
* 默认情况下，每个网格的样式是基本MeshStyle类的实例。
* 后者提供了Starling的标准渲染功能：绘制彩色或有纹理映射的三角形。

要使用新的网格样式，你可以扩展MeshStyle。
这允许您为各种有趣的效果创建自定义着色器程序。
例如，您可以实现快速颜色转换或多纹理。

[TIP]
====
一个最令人印象深刻的样式之一是 http://wiki.starling-framework.org/extensions/dynamic_lighting[动态照明扩展].
借助于法线贴图（针对表面法线编码的纹理），它可以提供逼真的实时照明效果。
一定要在Starling Wiki看看这个扩展！
====

要使用样式，实例化它并将其分配给网格的“style”属性：

[source, as3]
----
var image:Image = new Image(texture);
var lightStyle:LightStyle = new LightStyle(normalTexture);
image.style = lightStyle;
----

.动态照明扩展。
image::dynamic-lighting.png[Dynamic Lighting]

样式几乎无所不能;它们的应用无极限。
并且由于具有相同样式的网格可以分批在一起，您不必担心因此牺牲性能。
在这方面，它们比片段过滤器（它们具有类似的目的）更高效。

样式的主要缺点是它们只能被分配给一个网格物体（不是一个精灵），并且它们只能在实际的网格区域内作用（比如模糊效果就不能实现）。
此外，不能在一个网格上组合几个样式。

虽然如此，样式仍然是任何Starling开发人员都应该熟悉的一个强大的工具。
在“自定义样式，稍后部分”中，我将向您介绍如何从头开始创建自己的网格样式，着色器等等！敬请期待!

[NOTE]
====
如果你对于Mesh和MeshStyle之间的区别仍然有点困惑，可以这样想：
Mesh只是一个存储顶点列表，以及描述这些顶点如何构成三角形的对象。

MeshStyle则可以向每个顶点添加附加数据，并在渲染时使用它们。
标准MeshStyle提供颜色和纹理坐标; MultiTextureStyle可能会添加一组额外的纹理坐标等。
但是样式不应该修改对象的原始形状;它不会添加或删除顶点或更改其位置。
====
