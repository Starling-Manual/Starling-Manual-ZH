== 动态文本
ifndef::imagesdir[:imagesdir: ../../img]

文本是每个应用程序的重要组成部分。
你用图像传达的信息很有限; 一些事情只能依靠运行时动态地用语言描述。

=== TextFields

Starling可以方便地显示动态文本。
TextField类字面意思应该非常明确！

[source, as3]
----
var textField:TextField = new TextField(100, 20, "text"); // <1>
textField.format.setTo("Arial", 12, Color.RED); // <2>
textField.format.horizontalAlign = Align.RIGHT; // <3>
textField.border = true; // <4>
----
<1> 创建一个大小为100×20点的TextField，显示文本“text”。
<2> 我们将格式设置为“Arial”，大小为12点，红色。
<3> 文本向右对齐。
<4> border属性在主要开发阶段有用：它将显示TextField的边界。

NOTE: 注意，文本的样式是通过`format`属性设置的，它指向`starling.text.TextFormat`的一个实例。

一旦创建TextField，您可以像使用图像(Image)或四边形(Quad)一样使用它。

.Starling的文本呈现功能的一些示例。
image::textfield-samples.png[TextField Samples, 320]

=== TrueType字体

默认情况下，Starling将使用系统字体来渲染文本。
例如，如果设置TextField使用“Arial”字体，它将使用系统上安装的（如果系统中包含）。

然而，该方法的渲染质量不是最优的; 例如，可以在没有抗锯齿的情况下呈现字体。

为了获得更好的输出，应该将TrueType字体直接嵌入到SWF文件中。
使用以下代码：

[source, as3]
----
[Embed(source="Arial.ttf", embedAsCFF="false", fontFamily="Arial")]
private static const Arial:Class; // <1>

[Embed(source="Arial Bold.ttf", embedAsCFF="false", fontFamily="Arial", fontWeight="bold")]
private static const ArialBold:Class; // <2>

[Embed(source="Arial Italic.ttf", embedAsCFF="false", fontFamily="Arial", fontStyle="italic")]
private static const ArialItalic:Class; // <3>

[Embed(source="Arial.ttf", embedAsCFF="false", fontFamily="Arial", unicodeRange = "U+0020-U+007e")]
private static const ArialJustLatin:Class; // <4>
----
<1> 嵌入标准Arial字体。注意embedAsCFF部分：不要跳过！否则，字体根本不会显示。
<2> 粗体和斜体样式必须单独嵌入。注意fontWeight属性在这里，
<3> fontStyle属性也在这里。
<4> 当您不需要所有Unicode字符时，您还可以定义要包括哪些字形，这对于大字符集语言字体库很有用。
    此处显示的范围是基本拉丁语（大写和小写字符，数字和公共符号/标点符号）。

嵌入字体后，所有使用相应字体名称（字体系列）和权重设置的TextField都将自动使用嵌入字体。
除此之外不需要其他设置或配置。

TIP: 当嵌入字体的所有字形时，当心占用过大的空间。
上面显示的“unicodeRange”减轻了这个问题。您可以使用例如 http://renaun.com/blog/2011/10/flash-embed-font-unicode-range-generator[Unicode Range Generator].

TIP: 如果您的文本被裁剪或出现在错误的位置，请查看您当前的“stage.quality”设置。
低品质通常导致Flash / AIR返回不正确的文本边界值，而Starling在绘制文本时取决于这些值。
（在这里谈论的是指Flash原生stage,以上只适用于TrueType字体。）

=== 位图字体

使用如上所示的TrueType字体适用于不经常更改的文本。
但是，如果您的TextField不断更改其内容，或者如果要显示TrueType格式不可用的花哨字体，则应使用位图字体。

位图字体是包含要显示的所有字符的纹理。
与TextureAtlas类似，XML文件存储着字形处于纹理中的位置。

这是Starling渲染的位图字体的所有必要条件。
要创建所需的文件，有几个途径：

* http://kvazars.com/littera/[Littera], 一个功能齐全的免费在线位图字体生成器。
* http://www.angelcode.com/products/bmfont/[Bitmap Font Generator], 一个由AngelCode提供的工具，它允许您使用任何TrueType字体创建位图字体。但是，它仅适用于Windows。
* http://glyphdesigner.71squared.com[Glyph Designer] 为macOS，一个优秀的工具，允许你添加漂亮的特殊效果到您的字体。
* http://www.bmglyph.com[bmGlyph], 也专用于macOS，在App Store上可用。

这些工具使用方法都类似，允许您选择一个系统字体并可选地应用一些特殊效果。
导出文件时，有几件事要注意：

* Starling需要“.fnt”格式的XML变体。
* 确保选择正确的字形集;否则，你的字体纹理可能会变得非常大。

结果是“.fnt”文件和包含字符的关联纹理。

.包含颜色和阴影的位图字体。
image::desyrel-font.png[Bitmap Font, 400]

要使这样的字体可用于Starling，您可以将其嵌入到SWF中并将其注册到TextField类。

[source, as3]
----
[Embed(source="font.png")]
public static const FontTexture:Class;

[Embed(source="font.fnt", mimeType="application/octet-stream")]
public static const FontXml:Class;

var texture:Texture = Texture.fromEmbeddedAsset(FontTexture);
var xml:XML = XML(new FontXml());
var font:BitmapFont = new BitmapFont(texture, xml); // <1>

TextField.registerCompositor(font); // <2>
----
<1> 创建BitmapFont类的实例。
<2> 在TextField类中注册字体。

一旦位图字体实例在TextField类中完成注册，则无需再进一步处理它。
Starling将在遇到使用具有该名称的字体的TextField时，简单地拾取该字体。
像这样：

[source, as3]
----
var textField:TextField = new TextField(100, 20, "Hello World");
textField.format.font = "fontName"; // <1>
textField.format.fontSize = BitmapFont.NATIVE_SIZE; // <2>
----
<1> 要使用字体，只需引用字体名称。默认情况下，名称存储在XML文件的`face`属性中。
<2> 当字体大小与创建字体纹理时的实际字体大小一致时，位图字体看起来最好。你可以手动分配这个大小 - 但更好的做法是通过`NATIVE_SIZE`常量来设置。

==== 窍门

还有一件事你需要知道：如果你的位图字体只使用一种颜色（像一个正常的TrueType字体，没有任何颜色效果），你的字形需要导出为纯白色。
然后，通过设置TextField的`format.color`属性可以用来在运行时将字体颜色调整为任意颜色（简单地通过与纹理的RGB通道相乘）。

另一方面，如果你的字体包含颜色（像上面的示例图像），需要将TextField的`format.color`属性设置为白色（`Color.WHITE`）。
这样，TextField的颜色着色将不会影响纹理颜色。

TIP: 为了获得最佳性能，您甚至可以向纹理图集中添加字体纹理！
这样，您的文本可以与常规图像一起批处理，减少drawCall的调用。

==== MINI字体

Starling实际上带有一个非常轻量级的位图字体。
它可能不会赢得任何选美比赛 - 但当你仅仅需要显示最基本的文本，或者作为一些调试输出时，它是最合适的。

.“MINI”位图字体。
image::mini-font.png[BitmapFont.MINI, 396]

当我说轻便的，我的意思是：每个字母只有5像素高。
有一个窍门，但是，它会扩展到正好200％的原生大小。

[source, as3]
----
var textField:TextField = new TextField(100, 10, "The quick brown fox ...");
textField.format.font = BitmapFont.MINI; // <1>
textField.format.fontSize = BitmapFont.NATIVE_SIZE * 2; // <2>
----
<1> 使用MINI字体
<2> 使用原始大小的两倍。 由于字体使用最近邻的缩放，它仍然保持清晰！
