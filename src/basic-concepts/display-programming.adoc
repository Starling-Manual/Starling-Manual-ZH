== 显示编程
ifndef::imagesdir[:imagesdir: ../../img]

完成了所有的设置过程，我们可以开始实际把一些内容到显示到屏幕上！

在您创建的每个应用程序中，您的主要任务之一是将其拆分为多个逻辑单元。
通常，这些单元将具有可视化表示形式。
换句话说：每个单元将是一个显示对象。

=== 显示对象

屏幕上显示的所有元素其类型都是显示对象。
`starling.display`包包括抽象_DisplayObject_类;它提供了许多不同类型的基本显示对象，例如图像，影片剪辑和文本等等。

_DisplayObject_类提供了所有显示对象共有的方法和属性。
例如，以下属性用于配置对象在屏幕上的位置：

* `x`，`y`：当前坐标系中的位置。
* `width`，`height`：对象的大小（以点为单位）。
* `scaleX`，`scaleY`：另一种方式来查看对象大小; “1.0”表示未缩放，“2.0”将尺寸加倍等。
* `rotation`：对象绕其原点旋转（以弧度表示）。
* `skewX`，`skewY`：水平和垂直倾斜（以弧度表示）。

其他属性修改像素在屏幕上的显示方式：

* `blendMode`：确定对象的像素与下面的像素混合。
* `filter`：修改对象外观的特殊GPU程序（着色器）。过滤器可以模糊对象或添加投影。
* `mask`：mask删除某个区域外的所有部分。
* `alpha`：对象的不透明度，从“0”（不可见）到“1”（完全不透明）。
* `visible`：如果为false，则对象将完全隐藏。

以上这些是每个显示对象必须支持的基本属性。
下面让我们看看Starling关于这方面的API架构：

[plantuml, class-hierarchy]
....
include::../defaults.plantuml[]

abstract class DisplayObject
abstract class DisplayObjectContainer

DisplayObject <|-- Mesh
DisplayObject <|-- DisplayObjectContainer
DisplayObjectContainer <|-- Stage
DisplayObjectContainer <|-- Sprite
DisplayObjectContainer <|-- Button
DisplayObjectContainer <|-- TextField
Mesh <|-- Quad
Quad <|-- Image
Image <|-- MovieClip
....

你会注意到，图表分成两个主要的子分支。
第一个分支，有一些类扩展自`Mesh`：例如`Quad`，`Image`和`MovieClip`。

网格是Starling的渲染架构的基本组成部分。
实际上一切被绘制到屏幕上的对象都是网格！
_Stage3D_不能绘制任何东西，除了三角形，一个网格其实是一系列的点，这些点按一定的顺序组成三角形。

第二个分支，你会发现一些扩展自_DisplayObjectContainer_的类。
顾名思义，这个类充当其他显示对象的容器。
它可以将显示对象组织成逻辑系统 - _display list_（显示列表）。

=== 显示列表

被渲染的所有显示对象的层次结构称为_display list_。
_Stage_是显示列表的根容器。
把"stage"(舞台)按字面上的意思理解：你的用户（观众）只会看到已经进入舞台的对象（演员）。
当您开始创建Starling时，舞台将自动被创建。
连接到舞台（直接或间接）的所有内容都将被渲染。

当我说“连接到”时，我的意思是需要一个_parent-child_关系。
为了使一个对象出现在屏幕上，你应该把它添加(addChild)到舞台，或任何其他连接到舞台的_DisplayObjectContainer_上。

.Display objects are organized in the display list.
image::display-list.png[Display List, 350]

舞台的第一个（通常是唯一的）子对象是_application root_：这是你传递给Starling构造函数的类。
就像舞台，它可能也是一个_DisplayObjectContainer_。
你将通过它来接管显示列表！

您将创建容器，其中容器将包含其他容器和网格（例如图片）。
在树型的显示列表中，这些网格构成叶子：它们不能再包含任何子对象。

这一切听起来似乎很抽象，让我们看一个具体的例子：一个聊天气泡。
要创建聊天气泡，您需要一个图像（对于气泡）和一些文本（对于讲话内容）。

这两个对象应该作为一个整体：当移动时，图像和文本应该跟随。
同样的，应该一起适应大小，缩放，旋转等的变化。
我们可以把这些对象放在一个非常轻量级的_DisplayObjectContainer_：_Sprite_中来实现。

[NOTE]
.DisplayObjectContainer vs. Sprite
====
_DisplayObjectContainer_和_Sprite_可以几乎具有相同的意义。
这两个类之间的唯一区别是，一个（_DisplayObjectContainer_）是抽象的，而另一个（_Sprite_）不是。
因此，您可以使用_Sprite_将对象分组在一起，而不需要再创建一个扩展自_DisplayObjectContainer_的子类。
可以看出_Sprite_的另一个优点：它是更快捷的类型。
通常，这是我更喜欢它的主要原因。
像大多数程序员一样，我也是一个懒人！
====

因此，要将文本和图像组合在一起，您可以创建一个sprite，并将文本和图像添加为_children_：

[source, as3]
----
var sprite:Sprite = new Sprite(); // <1>
var image:Image = new Image(texture);
var textField:TextField = new TextField(200, 50, "Ay caramba!");
sprite.addChild(image); // <2>
sprite.addChild(textField); // <3>
----
<1> 创建sprite。
<2> 向sprite中添加一个_Image_。
<3> 向sprite中添加一个_TextField_。

添加子项的顺序很重要 - 它们按照放置顺序来决定显示层次。
这里，`textField`将出现在`image`前面。

.一个聊天气泡，由一个图像和一个TextField组成。
image::speech-bubble.png[Speech Bubble, 600]

现在那些对象被分组到同一个sprite里了，你可以把sprite当成一个整体来使用。

[source, as3]
----
var numChildren:int = sprite.numChildren; // <1>
var totalWidth:Number = sprite.width; // <2>
sprite.x += 50; // <3>
sprite.rotation = deg2rad(90); // <4>
----
<1> 查询子对象个数。 这里，结果将是“2”。
<2> `width`和`height`计算了子对象的大小和位置。
<3> 向右移动50点。
<4> 将组旋转90度（Starling始终使用弧度）。

实际上，_DisplayObjectContainer_定义了许多方法来帮助你操纵它的子对象：

[source, as3]
----
function addChild(child:DisplayObject):void;
function addChildAt(child:DisplayObject, index:int):void;
function contains(child:DisplayObject):Boolean;
function getChildAt(index:int):DisplayObject;
function getChildIndex(child:DisplayObject):int;
function removeChild(child:DisplayObject, dispose:Boolean=false):void;
function removeChildAt(index:int, dispose:Boolean=false):void;
function swapChildren(child1:DisplayObject, child2:DisplayObject):void;
function swapChildrenAt(index1:int, index2:int):void;
----

=== 坐标系

每个显示对象都有自己的坐标系。
例如，显示对象的`x`和`y`属性并不是直接针对屏幕坐标系而言，它们总是取决于当前的坐标系。
该坐标系又取决于对象在显示列表层次结构中的位置。

为了理解这一点，想象将纸片固定在书签板上。
每张纸版都表示有水平x轴和垂直y轴的坐标系。
你插入钉针的位置就是该坐标系的原点。

.坐标系统的工作方式类似于书签板.
image::coordinate-systems.png[Coordinage Systems]

现在，当您旋转纸张时，绘制在其上的所有内容（例如图像和文本）将随其一起旋转，如同旋转了它的x和y轴一样。
然而，坐标系的原点（针）仍保持在它的位置。

因此，针的位置实际上是纸张相对于其母坐标系统（书签板）所确定的x坐标和y坐标。

在创建显示层次结构时，请牢记书签板的例子。
当采用Starling开发时这是一个你需要理解的非常重要的概念。

=== 自定义显示对象

我已经提到过：当你创建一个应用程序时，你把它拆分成不同的逻辑部分。
一个简单的国际象棋游戏可能包含棋盘，棋子，暂停按钮和消息框等部分。
所有这些对象将显示在屏幕上 - 因此，每个对象都将由从DisplayObject派生的类来表示。

以一个简单的消息框为例。

.游戏的消息框。
image::msgbox.png[Message Box]

这实际上与我们刚刚创建的讲话泡泡非常相似; 除了背景图像和文本，它还包含两个按钮。

这一次，我们不是仅仅将对象组合在一个sprite中，而是将它封装成一个方便的类，并且隐藏任何实现细节。

为了实现这一点，我们创建一个继承自DisplayObjectContainer的新类。
在其构造函数中，我们创建构成消息框的所有内容：

[source, as3]
----
public class MessageBox extends DisplayObjectContainer
{
    [Embed(source = "background.png")]
    private static const BackgroundBmp:Class;

    [Embed(source = "button.png")]
    private static const ButtonBmp:Class;

    private var _background:Image;
    private var _textField:TextField;
    private var _yesButton:Button;
    private var _noButton:Button;

    public function MessageBox(text:String)
    {
        var bgTexture:Texture = Texture.fromEmbeddedAsset(BackgroundBmp);
        var buttonTexture:Texture = Texture.fromEmbeddedAsset(ButtonBmp);

        _background = new Image(bgTexture);
        _textField  = new TextField(100, 20, text);
        _yesButton  = new Button(buttonTexture, "yes");
        _noButton   = new Button(buttonTexture, "no");

        _yesButton.x = 10;
        _yesButton.y = 20;
        _noButton.x  = 60;
        _noButton.y  = 20;

        addChild(_background);
        addChild(_textField);
        addChild(_yesButton);
        addChild(_noButton);
    }
}
----

现在你有一个简单的类，包含一个背景图像，两个按钮和一些文本。
要使用它，只需创建一个MessageBox的实例并将其添加到显示列表：

[source, as3]
----
var msgBox:MessageBox = new MessageBox("Really exit?");
addChild(msgBox);
----

您可以向类添加其它方法（如fadeIn和fadeOut），以及在用户单击其中一个按钮时触发的代码。
这是使用Starling的事件机制来完成的，这将在后面的章节中讲到。

=== 移除显示对象

当您不想再显示某个对象时，只需将其从其父级中删除即可。 例如通过调用`removeFromParent()`。
当然，这种情况下该对象仍然会存在，如果你想复用的话,你可以将它添加到另一个显示对象容器中。
然而，很多时候，该对象没有继续存在的必要。
在这种情况下，销毁它是一个好的做法。

[source, as3]
----
msgBox.removeFromParent();
msgBox.dispose();
----

当销毁显示对象时，它们将释放其本身（及任何子级）占用的所有资源。
这很重要，因为许多Stage3D相关的数据是无法被垃圾回收器回收的。
如果您没有销毁这些数据，它们将一直被保留在内存中，这意味着应用程序迟早会耗尽内存资源而崩溃。

为了处理起来更简单，`removeFromParent（）`接受一个可选地布尔参数来决定是否销毁该DisplayObject。
这样，上面的代码可以简化为这一行：

[source, as3]
----
msgBox.removeFromParent(true);
----

=== 枢轴点

枢轴点是在传统显示列表中找不到的功能。
在Starling中，显示对象包含两个附加属性：pivotX和pivotY。
对象的枢轴点（也称为_origin_，_root_或_anchor_）定义其坐标系的根。

默认情况下，枢轴点在（0，0）`; 在图像中，即左上角位置。
大多数时候，这是适合需求的。
然而，有时，你想要在不同的位置 - 例如。 当您想要围绕其中心旋转图像时。

你必须将对象包装在容器中，以便使用枢轴点：

[source, as3]
----
var image:Image = new Image(texture);

var sprite:Sprite = new Sprite(); // <1>
image.x = -image.width / 2.0;
image.y = -image.height / 2.0;
sprite.addChild(image); // <2>

sprite.rotation = deg2rad(45); // <3>
----
<1> 创建sprite。
<2> 添加一个图像，使其中心正好在sprite的原点上。
<3> 旋转sprite会使图像围绕其中心旋转。

大多数有长期Flash开发经验的人员都会知道这个技巧; 并且会定期的使用它。
然而，有人可能会说，做这样一个很简单的事情也需要写不少代码。
借助于枢轴点，代码将缩减为以下形式：

[source, as3]
----
var image:Image = new Image(texture);
image.pivotX = image.width  / 2.0; // <1>
image.pivotY = image.height / 2.0; // <2>
image.rotation = deg2rad(45); // <3>
----
<1> 将pivotX移动到图像的水平中心。
<2> 将`pivotY`移动到图像的垂直中心。
<3> 绕中心旋转。

不需要创建多余的容器！
延续前面章节中使用的类比：枢轴点定义当您将其附加到父对象时，钉子穿过的位置。
上面的代码将枢轴点移动到对象的中心。

.Note 移动枢轴点会决定对象如何旋转。
image::pivot-point.png[Pivot Point]

现在你已经学会了如何单独控制枢轴点坐标，让我们来看看“alignPivot()”方法。
它允许我们只用一行代码将枢轴点移动到对象的中心：

[source, as3]
----
var image:Image = new Image(texture);
image.alignPivot();
image.rotation = deg2rad(45);
----

方便吗？

此外，如果我们想要把枢轴点置于其它地方（例如，在右下角），我们可以给该方法传递对齐参数：

[source, as3]
----
var image:Image = new Image(texture);
image.alignPivot(Align.RIGHT, Align.BOTTOM);
image.rotation = deg2rad(45);
----

该代码使图像围绕其右下角旋转。

==== 窍门

注意：枢轴点总是在对象的本地坐标系中给出。
这与'width'和'height'属性不同，它们实际上是相对于parent坐标系。
当枢轴点与对象的缩放或旋转等属性同时被设置时，结果会使人诧异。

例如，想象一个100像素宽，缩放到200％的图像（`image.scaleX = 2.0`）。
该图像现在返回一个宽度为200像素（宽度的两倍）的宽度。
但是，要将水平枢轴点居中，您仍然将`pivotX`设置为`50`，而不是`100`！
因为在本地坐标系中，图像仍然是100像素宽 - 它只是在parent坐标系中显得更宽。

当你回顾本节开头的代码时，我们可能会更容易理解，我们将图像集中在一个父容器中。
如果你改变了sprite的`scale`会发生什么？
这是否意味着您必须更新图像的位置以保持其居中？
当然不是。
scale不影响容器内部发生的事情，只是它从外部看起来变化了。
从这个方面讲它与枢轴点相同。

TIP: 如果你仍然头痛（实际上发生在我身上），只要记住在改变对象的scale或rotation属性之前先设置好枢轴点。
这将避免所有问题。
