== 动画

动画不仅是任何游戏的基本组成部分;也为现代商业应用程序提供平滑且生动的过渡。
显然，提供一套能快速响应设置且直观的接口对控制动画大有裨益。
正因为如此，Starling内置了一个非常灵活的动画引擎。

如你所想，动画可以简单分为两种类型。

* 第一种,动画变化非常动态，你无法预知会发生什么。比如有一个向玩家移动的敌人：它的方向和速度需要根据环境每一帧更新。每个额外的力量或碰撞都可能改变移动轨迹。

* 另一种,预定义动画，这种动画遵循一定的规律;你从开始就知道会发生什么。比如消息框中淡出,或者从一个屏幕过渡到另一个屏幕。

我们将在以下部分示例这两种类型。

=== 帧事件

在一些游戏引擎中，有一个叫做run-loop的机制。
这是一个无限循环，不断更新场景的所有元素。

在Starling中，由于显示列表架构，这样的循环没有什么意义。
我们已经将游戏分成许多不同的自定义显示对象，每个人都应该知道哪段时间该做什么。

这正是帧事件(EnterFrameEvent)的要点：允许显示对象随时间更新自身。
每帧，该事件被分派给位于显示列表中的所有显示对象。
下面讲如何使用它：

[source, as3]
----
public function CustomObject()
{
    addEventListener(Event.ENTER_FRAME, onEnterFrame); // <1>
}

private function onEnterFrame(event:Event, passedTime:Number):void // <2>
{
    trace("Time passed since last frame: " + passedTime);
    bird.advanceTime(passedTime);
}
----
<1> 您可以在任何地方向此事件添加侦听器，但构造函数是一个很好的选择。
<2> 这是对应的事件侦听器的样子。

方法`onEnterFrame`每帧被调用一次，并且它沿着自上一帧以来已经过去的时间传递。
有了这些信息，你可以移动你的敌人，更新太阳的高度，或做任何其他需要。

这个事件背后的蕴含力量是，每次事件发生时你可以做完全不同的事情。
你可以动态地响应游戏的当前状态。

例如，你可以让敌人向玩家迈进一步;甚至一个简单形式的敌人AI，如果你愿意！

=== Tweens

现在到预定义的动画。
它们非常常见，例如movement，scale，fade等。
Starling处理这些动画的方法很简单 - 但同时非常灵活。
基本上，你可以缓动任何对象的任何属性，只要它是数字（`Number`，`int`，`uint`）。
这些动画描述被封装在称为Tween的对象中。

NOTE: 术语“Tween”来自手工绘制的动画，其中铅笔插画将绘制重要的关键帧，而团队的其余人员绘制关键帧之间(in-between)的其它帧。

.渐变帧。
image::soccer-tween.png[Soccer Tween]

了解了这些理论，接下来我们举个例子：

[source, as3]
----
var tween:Tween = new Tween(ball, 0.5);

tween.animate("x", 20);
tween.animate("scale", 2.0);
tween.animate("alpha", 0.0);
----

这个补间描述了一个动画，将“ball”对象移动到“x = 20”，将其放大两倍，并降低其不透明度，直到它不可见。
所有这些变化将在半秒内同时进行。
起始值仅仅是指定属性的当前值。

这个示例告诉我们：

* 你可以对一个对象的任意属性进行动画处理
* 您可以在一个补间对象中组合多个动画。

Apropos: 由于缩放，淡入淡出和移动的频率很高，所以Tween类也提供了更为具体的方法。
你可以这样写:

[source, as3]
----
tween.moveTo(20, 0); // animate "x" and "y"
tween.scaleTo(2);    // animate "scale"
tween.fadeTo(0);     // animate "alpha"
----

补间动画的一个有趣的功能是，你可以改变动画的执行方式，例如。 让它先慢后快。
这是通过指定过渡类型做到的。

.可用的过渡类型。 缺省值为“linear”。
image::transitions.png[Transitions]

以下示例显示如何指定这样的转换，并介绍此类的更多功能。

[source, as3]
----
var tween:Tween = new Tween(ball, 0.5, Transitions.EASE_IN); // <1>
tween.onStart    = function():void { /* ... */ };
tween.onUpdate   = function():void { /* ... */ }; // <2>
tween.onComplete = function():void { /* ... */ };
tween.delay = 2; // <3>
tween.repeatCount = 3; // <4>
tween.reverse = true;
tween.nextTween = explode; // <5>
----
<1> 通过第三个构造函数参数指定过渡类型。
<2> 这些回调在补间启动时，每帧更新时或完成时执行。
<3> 延迟两秒钟再执行动画。
<4> 重复播放动画三次，可选择以yoyo风格（`reverse`）。 如果将`repeatCount`设置为零，动画将被无限重复。
<5> 指定下一个动画补间，在此动画完成后立即开始。

我们刚刚创建和配置了一个补间动画 - 但什么也没有发生。
一个tween对象描述了动画如何运行，但它并未开始运行。

你可以通过tweens`advanceTime`方法手动执行：

[source, as3]
----
ball.x = 0;
tween = new Tween(ball, 1.0);
tween.animate("x", 100);

tween.advanceTime(0.25); // -> ball.x =  25
tween.advanceTime(0.25); // -> ball.x =  50
tween.advanceTime(0.25); // -> ball.x =  75
tween.advanceTime(0.25); // -> ball.x = 100
----

嗯，它们正常工作了，但这样编码有点麻烦，不是吗？
虽然可以在一个`ENTER_FRAME`事件响应函数中调用`advanceTime`，但是只要你有多个动画，它仍然一定会变得繁琐。

别担心：我知道有个好家伙。
他真的善于处理这样的事情。

=== Juggler

Juggler(魔术师)接受并执行任意数量的动画对象。
像任何真正的艺术家一样，它将以饱满的热情坚定地追求其生命价值：连续调用`advanceTime`。

在活动Starling实例上总是有一个默认的juggler。
执行动画的最简单的方法是通过下面这行代码 - 只要将动画分配给默认的魔术师，你就完成了。

[source, as3]
----
Starling.juggler.add(tween);
----

当动画完成后，它将被自动释放，无需后续设置。
在许多情况下，这种简单的方法将是你需要的！

在某些情况下，你需要更多的控制。
假设你的舞台包含一个主要动作发生的游戏区域。
当用户点击暂停按钮时，您想要暂停游戏并显示一个消息框（消息框本身也有动画），也许再提供一个选项返回到游戏菜单。

当这种情况发生时，游戏应该完全冻结：它的动画不应该再继续。
问题是消息框本身也使用一些动画，所以我们不能只让默认的魔术师停止工作。

在这种情况下，给游戏区域额外分配一个魔术师就变得有必要。
一旦按下退出按钮，这个魔术师应该停止一切工作。
游戏将冻结在当前画面，而消息框（使用默认的魔术师，或者可能是另一个）动画仍然在工作。

当你创建一个自定义的魔术师，你所要做的就是在每一帧中调用它的“advanceTime”方法。
我建议通过以下方式使用jugglers：

[source, as3]
----
public class Game // <1>
{
    private var _gameArea:GameArea;

    private function onEnterFrame(event:Event, passedTime:Number):void
    {
        if (activeMsgBox)
            trace("waiting for user input");
        else
            _gameArea.advanceTime(passedTime); // <2>
    }
}

public class GameArea
{
    private var _juggler:Juggler; // <3>

    public function advanceTime(passedTime:Number):void
    {
        _juggler.advanceTime(passedTime); // <4>
    }
}
----
<1> 在你的游戏的根类，监听`Event.ENTER_FRAME`。
<2> 仅当没有活动消息框时才推进`gameArea'。
<3> GameArea包含自己的魔术师。 它将管理所有的游戏内动画。
<4> 玩家在其“advanceTime”方法（由Game调用）中前进。

这样，你已经完美地分离了游戏的动画和消息框。

顺便说一下：魔术师不限于Tweens家族。
只要一个类实现了“IAnimatable”接口，就可以将它列入到魔术师阵营。
该接口只有一个方法：

[source, as3]
----
function advanceTime(time:Number):void;
----

通过实现此接口，您可以 自己创建一个简单的MovieClip类。
在它的“advanceTime”方法中，它会不断地改变显示的纹理。
要启动影片剪辑，您只需将其分配给魔术师。

这留下一个问题，那就是：什么时候并且如何从魔术师中删除一个动画对象？

==== 停止动画

当动画完成后，它会自动移除。
如果要在动画完成之前中止动画，则只需从魔术师中删除这个动画。

让我们看看一个例子，创建一个动画：球移动，并将它分配给默认魔术师：

[source, as3]
----
tween:Tween = new Tween(ball, 1.5);
tween.moveTo(x, y);
Starling.juggler.add(tween);
----

下面列举了几种可以中止该动画方法。
你只需选择最适合您的当前游戏逻辑的一个。

[source, as3]
----
var animID:uint = juggler.add(tween);

Starling.juggler.remove(tween); // <1>
Starling.juggler.removeTweens(ball); // <2>
Starling.juggler.removeByID(animID); // <3>
Starling.juggler.purge(); // <4>
----
<1> 直接删除动画。 这适用于任何`IAnimatable`对象。
<2> 删除影响球的所有动画。 只适用于tweens！
<3> 通过其ID删除动画。 当您无法访问Tween实例时很有用。
<4> 如果你想中止一切，让魔术师停止工作。

但是有一点小心，“purge”的方法，会使你的所有依赖默认魔术师的动画嘎然而止，显得很突兀。
我建议你只在自定义的魔术师身上使用“purge”方法。

==== 自动删除

你可能已经问自己，如何知道Tween类在动画完成后自动从运行时中删除了？
这可以通过`REMOVE_FROM_JUGGLER`事件来获取通知。

实现“IAnimatable”的任何对象都可以调度这样的事件; 玩家监听这些事件并相应地删除对象。

[source, as3]
----
public class MyAnimation extends EventDispatcher implements IAnimatable
{
    public function stop():void
    {
        dispatchEventWith(Event.REMOVE_FROM_JUGGLER);
    }
}
----

==== 单命令Tweens

虽然tween和juggler的分离是非常强大的功能，但它有时这个分离反而迫使你为简单的任务写了很多代码。
这就是为什么在魔术师身上有一个方便的方法，允许您使用单个命令创建和执行补间。
下面是一个示例：

[source, as3]
----
juggler.tween(msgBox, 0.5, {
   transition: Transitions.EASE_IN,
   onComplete: function():void { button.enabled = true; },
   x: 300,
   rotation: deg2rad(90)
});
----

这将为`msgBox`对象创建一个动画，持续时间为0.5秒，同时为`x`和`rotation`属性生成动画。
正如你所看到的，`{}`-parameter用来列出你想要动画的所有属性，以及Tween本身的属性。
非常节省时间！

=== 延迟调用

从技术上讲，我们现在已经涵盖了Starling支持的所有动画类型。
然而，实际上还有另一个概念与这个主题密切相关。

记住Einstein，给我们介绍事件系统的那位智能狗？
最后一次我们看到他，他刚刚失去了所有的健康点，并打算叫“gameOver”。
但等待：不要立即调用该方法 - 这将突然结束游戏。
相反，调用它的延迟，例如，两秒钟（足够的时间，让玩家实现正在展开的剧情）。

要实现该延迟，您可以使用flash自带的Timer或`setTimeout`方法。
然而，你也可以使用魔术师，这有一个巨大的优势：你完全掌控一切。

当你想象玩家现在点击“暂停”按钮，在这两秒过去之前变得很明显。
在这种情况下，你不仅想要停止游戏区的动画;你希望这个延迟的`gameOver`调用被延迟更多。

为此，请进行如下操作：

[source, as3]
----
juggler.delayCall(gameOver, 2);
----

`gameOver`函数将从现在开始调用两秒钟（如果魔术师工作被中断，则更长时间）。
也可以给这个函数传递一些参数。
想要派发事件吗？

[source, as3]
----
juggler.delayCall(dispatchEventWith, 2, "gameOver");
----

使用延迟调用的另一种方便的方法是执行周期性操作。
想象你想要每三秒产生一个新的敌人。

[source, as3]
----
juggler.repeatCall(spawnEnemy, 3);
----

[NOTE]
====
在幕后，`delayCall`和`repeatCall`都创建一个DelayedCall类型的对象。
就像juggler.tween方法是使用tweens的快捷方式，这些方法是创建延迟调用的快捷方式。
====

要中止延迟调用，请使用以下方法之一：

[source, as3]
----
var animID:uint = juggler.delayCall(gameOver, 2);

juggler.removeByID(animID);
juggler.removeDelayedCalls(gameOver);
----

=== 影片剪辑

当我们查看关于Mesh的类图的时候，你可能已经注意到MovieClip类。
事实如此：MovieClip实际上只是Image的一个子类，随着时间的推移改变其纹理。
可以认为他是Starling中的一个GIF动画！

==== 获取纹理

建议您的影片剪辑的所有帧都来自一个纹理图集，并且所有帧都具有相同的大小（如果没有，它们将被拉伸到第一帧的大小）。
您可以使用像Adobe Animate这样的工具来创建这样的动画; 它可以直接导出为Starling的纹理图集格式。

这是包含影片剪辑的帧的纹理图集的示例。
首先，看看带有帧坐标的XML。
请注意，每个帧都以前缀`flight_`开头。

[source, xml]
----
<TextureAtlas imagePath="atlas.png">
    <SubTexture name="flight_00" x="0"   y="0" width="50" height="50" />
    <SubTexture name="flight_01" x="50"  y="0" width="50" height="50" />
    <SubTexture name="flight_02" x="100" y="0" width="50" height="50" />
    <SubTexture name="flight_03" x="150" y="0" width="50" height="50" />
    <!-- ... -->
</TextureAtlas>
----

这里是相应的纹理：

.MovieClip的帧。
image::flight-animation.png[Flight Animation]

==== 创建影片剪辑

现在让我们创建影片剪辑。
假设“atlas”变量指向包含上述影片剪辑所有帧的TextureAtlas，这很容易。

[source, as3]
----
var frames:Vector.<Texture> = atlas.getTextures("flight_"); // <1>
var movie:MovieClip = new MovieClip(frames, 10); // <2>
addChild(movie);

movie.play();
movie.pause(); // <3>
movie.stop();

Starling.juggler.add(movie); // <4>
----
<1> `getTextures`方法返回从给定前缀开始的所有纹理，按字母顺序排序。
<2> 这是我们MovieClip的理想选择，因为我们可以将这些纹理传递给它的构造函数。第二个参数描述了每秒播放多少帧。
<3> 这些是控制剪辑的回放的方法。默认情况下，它将处于“播放”模式。
<4> 重要提示：就像Starling中的任何其他动画一样，影片剪辑需要分配给魔术师！

你注意到我们通过他们的前缀`flight_`引用了图集的纹理吗？
这允许您创建包含其他影片剪辑和纹理的混合图集。
要将一个剪辑的帧组合在一起，您只需对所有剪辑使用相同的前缀。

该类还支持在达到某个帧时执行声音或任意回调。
请务必查看其API参考，了解可能的情况！

==== 更复杂的电影

这个动画技术的一个缺点必须提及，如果你的动画很长或者单个帧非常大，你将会耗尽纹理内存。
如果你的动画占用了几个大的纹理图集，它们可能因占用太多内存而不合适。

对于这些类型的动画，您需要切换到更复杂的解决方案：骨骼动画。
这意味着一个角色被分割成不同的部分（骨骼）;然后对于那些部分分别制作动画（根据角色的骨架）。
这是非常灵活的。

支持这样的动画不是Starling本身的一部分，但是有一些其他工具和库来协助。
以下所有的工作与Starling完全一样：

* http://esotericsoftware.com/[Spine]
* https://github.com/threerings/flump[Flump]
* http://dragonbones.github.io/[Dragon Bones]
* http://gafmedia.com[Generic Animation Format]
