== 实用工具

`starling.utils`包包含几个有用的小助手，不应该被忽视。

=== 颜色

在常规Flash和Starling中，颜色以十六进制格式指定。
这里有几个例子：

[source, as3]
----
// format:         0xRRGGBB
var red:Number   = 0xff0000;
var green:Number = 0x00ff00; // or 0xff00
var blue:Number  = 0x0000ff; // or 0xff
var white:Number = 0xffffff;
var black:Number = 0x000000; // or simply 0
----

Color类包含一些常用的颜色值的列表; 此外，您可以使用它来轻松访问各颜色通道数值。

[source, as3]
----
var purple:uint = Color.PURPLE; // <1>
var lime:uint   = Color.LIME;
var yellow:uint = Color.YELLOW;

var color:uint = Color.rgb(64, 128, 192); // <2>

var red:int   = Color.getRed(color);   // ->  64 <3>
var green:int = Color.getGreen(color); // -> 128
var blue:int  = Color.getBlue(color);  // -> 192
----
<1> 预定义几种常见的颜色。
<2> 可以使用此方法创建任何其他颜色。 只需将RGB值传递给此方法（范围：0 - 255）。
<3> 您还可以提取每个通道的整数值。

=== 角度

Starling期望所有角度以弧度表示（不同于Flash，在某些地方使用度数，在其他地方使用弧度）。
要在度数和弧度之间进行转换，可以使用以下简单函数。

[source, as3]
----
var degrees:Number = rad2deg(Math.PI); // -> 180
var radians:Number = deg2rad(180);     // -> PI
----

=== StringUtil

你可以使用`format`方法来格式化.Net/C#风格的字符串。

[source, as3]
----
StringUtil.format("{0} plus {1} equals {2}", 4, 3, "seven");
  // -> "4 plus 3 equals seven"
----

相同的类还包含从字符串的开头和结尾去除空格的方法 - 每当需要处理用户输入时进行频繁操作。

[source, as3]
----
StringUtil.trim("  hello world\n"); // -> "hello world"
----

=== SystemUtil

了解应用或游戏当前执行环境的信息通常很有用。
SystemUtil包含一些帮助执行该任务的方法和属性。

[source, as3]
----
SystemUtil.isAIR; // AIR or Flash?
SystemUtil.isDesktop; // desktop or mobile?
SystemUtil.isApplicationActive; // in use or minimized?
SystemUtil.platform; // WIN, MAC, LNX, IOS, AND
----

=== MathUtil

虽然该类主要是为了帮助处理一些几何问题，它还包含以下非常有用的帮助方法：

[source, as3]
----
var min:Number = MathUtil.min(1, 10); // <1>
var max:Number = MathUtil.max(1, 10); // <2>
var inside:Number = MathUtil.clamp(-5, 1, 10); // <3>
----
<1> 获取两个数字中最小的数字。结果：`1`
<2> 获取两个数字中最大的。结果：`10`
<3> 将数字（第一个参数）移动到特定范围。结果：`1`

如果你以前使用过AS3，你可能会想知道为什么我写了这些方法，提供了类似原生的数学类。

不幸的是，这些等价方法有副作用：每次您调用`Math.min`，它创建一个临时对象（至少当你为iOS编译你的应用程序时）。
这些替代品没有这种副作用，所以你应该总是喜欢他们。

=== 池

现在我们碰到了临时对象的主题，这是介绍你Pool类的最佳时机。

经验丰富的AS3开发人员知道任何对象分配都有一个代价：对象需要被垃圾回收。
这完全在幕后发生;你甚至大部分时间不会注意到它。

但是，当清理过程占用太多时间时，您的应用程序将卡住一段时间。
如果这种情况经常发生，它很快就会成为您的用户的一个烦恼。

避免这个问题的一个策略是回收您的对象并反复使用它们。
例如，像Point和Rectangle这样的类通常只需要很短的使用时间：创建它们，用一些数据填充它们，然后丢弃它们。

从现在开始，让Starling的Pool类处理这些对象。

[source, as3]
----
var point:Point = Pool.getPoint(); // <1>
doSomethingWithPoint(point);
Pool.putPoint(point); // <2>

var rect:Rectangle = Pool.getRectangle(); // <1>
doSomethingWithRectangle(rect);
Pool.putRectangle(rect); // <2>
----
<1> 从池中获取对象。 它替换了类上调用'new'。
<2> 当你不再需要它时，把它放回池中。

该类还以类似的样式支持Vector3D，Matrix和Matrix3D。

IMPORTANT: 始终确保get和put调用是平衡的。
如果你把太多的对象放入池中，从来不检取它们，它将随着时间的推移而填满，使用越来越多的内存。

=== 此外 ...

`starling.utilities`包包含更多的帮助，我可能一并在这里列出。
有关方法和类的完整列表，请参阅http://doc.starling-framework.org/core/starling/utils/package-detail.html [API参考]。
它值得一看！
