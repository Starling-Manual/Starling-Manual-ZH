== 纹理和图像

我们几次碰到过图像(Image)和纹理(Texture)类，确实如此：它们是Starling中最有用的类。
但是如何使用它们，两者之间有什么区别呢？

=== 纹理

纹理只是描述图像的数据 - 好比保存在数码相机上的文件。
你无法直接看到数据文件所记录的图像：毕竟它只是一系列的0和1。
您需要一个图像查看器来查看它，或将其发送到打印机。

纹理直接存储在GPU内存中，这意味着它们可以在渲染期间被高效的访问。
我们通常是直接嵌入(embed)到类或通过加载文件来得到纹理。您可以选择以下文件格式之一作为纹理格式：

PNG:: 最通用的文件格式。其无损压缩特别适用于具有大面积纯色的图像。建议作为默认纹理格式。

JPG:: 由于使用有损压缩算法，对于摄影（和照片）图像，文件占用字节数比PNG格式小。然而，缺少alpha通道严重限制了其适用性。仅推荐照片和大背景图片使用这种格式。

ATF:: 特别为Stage3D定制的格式。 ATF纹理需要很少的纹理内存并且具有非常快的加​​载速度;然而，它们也是有损压缩的，不能完全适合于所有类型的图像。我们将在后面的章节中更详细地讨论ATF纹理（参见“ATF纹理”）。

`starling.textures.Texture` 类包含了许多用于实例化纹理的工厂方法。
下面列举了几个工厂方法（为了读起来更清楚，我省略了参数）。

[source, as3]
----
public class Texture
{
    static function fromColor():Texture;
    static function fromBitmap():Texture;
    static function fromBitmapData():Texture;
    static function fromEmbeddedAsset():Texture;
    static function fromCamera():Texture;
    static function fromNetStream():Texture;
    static function fromTexture():Texture;
}
----

可能最常见的任务是通过位图创建纹理。
这似乎并不容易:

[source, as3]
----
var bitmap:Bitmap = getBitmap();
var texture:Texture = Texture.fromBitmap(bitmap);
----

通过嵌入位图创建纹理也很常见。
这可以用跟上述相同的方式完成：

[source, as3]
----
[Embed(source="mushroom.png")] // <1>
public static const Mushroom:Class;

var bitmap:Bitmap = new Mushroom(); // <2>
var texture:Texture = Texture.fromBitmap(bitmap); // <3>
----
<1> 嵌入位图。
<2> 实例化位图。
<3> 从位图创建纹理。

然而，有一个捷径，使工作进一步简化：

[source, as3]
----
[Embed(source="mushroom.png")] // <1>
public static const Mushroom:Class;

var texture:Texture = Texture.fromEmbeddedAsset(Mushroom); // <2>
----
<1> 嵌入位图。
<2> 直接从存储嵌入资源的类创建纹理。

[IMPORTANT]
.提示
====
这不仅节省代码，还能节省内存！

该`fromEmbeddedAsset`方法通过幕后的一些黑科技来减少性能损失，比传统的`fromBitmap`方法更加高效。
我们稍后将回到这个主题，但现在，只记得这是从嵌入式位图创建纹理的首选方法。
====

Texture类的另一个特性隐藏在不显眼的`fromTexture`方法中。
它允许您使用另一个纹理中指定区域的局部纹理。

是什么原因让这个特性很有用？事实上，在这个过程中没有任何像素被复制。
相反，创建的SubTexture仅存储对其父纹理的引用。
这种方案非常高效！

[source, as3]
----
var texture:Texture = getTexture();
var subTexture:Texture = Texture.fromTexture(
        texture, new Rectangle(10, 10, 41, 47));
----

很快，你会知道_TextureAtlas_类; 它基本上围绕这个功能构建。

=== 图像

我们现在有了一些纹理，但我们仍然不知道如何在屏幕上显示它们。
最简单的方法是使用_Image_类或其直系亲属之一。

让我们聚焦此族谱中的这一部分。

[plantuml, mesh-classes]
....
include::../defaults.plantuml[]

Mesh <|-- Quad
Quad <|-- Image
Image <|-- MovieClip
....

* Mesh是三角形的平面集合（记住，GPU只能绘制三角形）。
* 一个Quad是一个至少有两个三角形的集合，它构成一个四边形。
* 一个Image只是一个有着方便的构造函数和一些额外方法的四边形。
* MovieClip是一种随时间切换纹理的图像。

虽然所有这些类都自带运用纹理的方法，但是你经常使用的可能是Image类。
这是因为矩形纹理是最常见的，而Image类是使用它们的最简便的方式。

为了演示，让我告诉你如何通过Quad和Image显示一个纹理。

[source, as3]
----
var texture:Texture = Texture.fromBitmap(...);

var quad:Quad = new Quad(texture.width, texture.height); // <1>
quad.texture = texture;
addChild(quad);

var image:Image = new Image(texture); // <2>
addChild(image);
----
<1> 创建具有适当大小的四边形并分配纹理，或：
<2> 使用其标准构造函数创建图像。

虽然在上述两种情况下，幕后发生的事情是完全相同的。
就个人而言，我总是选择能够节省一些代码的方法。

.纹理映射到四边形。
image::texture-mapping.png[Texture-Mapping]

=== 一个纹理，多个图像

需要强调的是一个纹理可以映射到任意数量的图像（网格）。
事实上，这正是你应该做的：加载一个纹理，然后在应用程序的整个生命周期重复使用它。

[source, as3]
----
// 不要这样做！！
var image1:Image = new Image(Texture.fromEmbeddedAsset(Mushroom));
var image2:Image = new Image(Texture.fromEmbeddedAsset(Mushroom));
var image3:Image = new Image(Texture.fromEmbeddedAsset(Mushroom));

// 正确的做法是，创建一次纹理，保留引用并重复使用：
var texture:Texture = Texture.fromEmbeddedAsset(Mushroom));
var image1:Image = new Image(texture);
var image2:Image = new Image(texture);
var image3:Image = new Image(texture);
----

几乎所有的内存占用都来自纹理;如果你浪费了纹理内存，你将很快耗尽RAM。

=== 纹理图集

在之前所有的示例中，我们都是分别加载每个纹理。
然而，真正的应用程序实际上不应该这样做。
这是为什么呢？

* 对于高效的GPU渲染，Starling将渲染的Meshes一起进行批处理。但是，当纹理改变时，批处理被中断。
* 在某些情况下，Stage3D需要纹理的宽度和高度是2的幂。
  Starling规避了这个限制，但是如果你不遵守这个规则，你会使用更多的内存。

通过使用纹理图集，您可以避免纹理切换和二次方的限制。
所有纹理都在一个大的“超级纹理”内，而Starling会关注显示这个纹理的正确部分。

.纹理图集。
image::penguflip-atlas.png[Texture Atlas]

诀窍是让Stage3D使用这个大纹理而不是小的纹理，并且只将大纹理的一部分映射到对应的每个四边形。
这将非常有效的节省内存使用，浪费尽可能小的空间。
（一些其他框架调用此功能，例如Sprite Sheets。）

ifdef::backend-html5[]
TIP: “Texture Packer”的团队实际上创建了一个关于sprite sheet的介绍视频。
在这里观看: http://www.codeandweb.com/what-is-a-sprite-sheet[Sprite Sheet]
endif::[]

==== 创建纹理图集

每个SubTexture在XML文件中像这样定义：

[source, xml]
----
<TextureAtlas imagePath="atlas.png">
 <SubTexture name="moon" x="0" y="0" width="30" height="30"/>;
 <SubTexture name="jupiter" x="30" y="0" width="65" height="78"/>;
 ...
</TextureAtlas>;
----

如你所见，XML引用一个大纹理并定义多个SubTextures，每个SubTexture指向该纹理内的一个区域。
在运行时，你可以通过他们的名字引用这些SubTextures，就像它们本来就是独立的纹理一样。

但是怎么将所有的纹理合并成这样的图集？
幸运的是，你不必手动做; 有很多工具，将帮助你完成这项任务。
这里有两个候选人，但Google会带来更多。

* https://www.codeandweb.com/texturepacker/starling?source=gamua[TexturePacker] 是我个人的最爱。 你不会找到任何工具，能够这么完善的控制你的精灵图集，同时对Starling的支持是这么友好（ATF纹理，还有其它工具吗？）。
* http://renderhjs.net/shoebox/[Shoebox] 是一个用AIR构建的免费工具。 虽然它没有TexturePacker一样多的选项来创建纹理图集，但它包含许多相关功能，如位图字体创建和精灵提取。

==== 使用纹理图集

好吧：你现在有一个纹理图集。
但是你怎么用呢？
让我们从嵌入纹理和XML数据开始。

[source, as3]
----
[Embed(source="atlas.xml", mimeType="application/octet-stream")] // <1>
public static const AtlasXml:Class;

[Embed(source="atlas.png")] // <2>
public static const AtlasTexture:Class;
----
<1> 嵌入图集描述文件XML。 不要忘记指定mimeType。
<2> 嵌入图集纹理。

NOTE: 或者，您也可以从URL或磁盘加载这些文件（如果我们在谈论AIR）。
当我们讨论Starling的AssetManager时，我们将详细讨论。

有了这两个对象，我们可以创建一个新的TextureAtlas实例，并通过getTexture()方法访问所有SubTextures。
在游戏初始化时创建一次atlas对象，并在其生命周期中引用它。

[source, as3]
----
var texture:Texture = Texture.fromEmbeddedAsset(AtlasTexture); // <1>
var xml:XML = XML(new AtlasXml());
var atlas:TextureAtlas = new TextureAtlas(texture, xml);

var moonTexture:Texture = atlas.getTexture("moon"); // <2>
var moonImage:Image = new Image(moonTexture);
----
<1> 创建图集。
<2> 显示子纹理(SubTexture).

就这么简单！

=== 渲染纹理

RenderTexture类允许动态创建纹理。
把它想象成一个画布，你可以在其上绘制任何显示对象。

创建渲染纹理后，只需调用`drawObject`方法将对象直接渲染到纹理上。
对象将被绘制到当前位置的纹理上，并附带其当前旋转，缩放和透明度等属性。

[source, as3]
----
var renderTexture:RenderTexture = new RenderTexture(512, 512); // <1>

var brush:Sprite = getBrush(); // <2>
brush.x = 40;
brush.y = 120;
brush.rotation = 1.41;

renderTexture.draw(brush); // <3>
----
<1> 创建一个具有给定大小（以点为单位）的新RenderTexture。 它将用完全透明的像素初始化。
<2> 在这个示例中，我们引用了一个描述画笔的显示对象。 我们将它移动到某个位置。
<3> 画刷对象将以其当前的位置和方向被绘制到纹理中。

绘图过程非常高效，因为它直接发生在图形存储器中。
在将对象绘制到渲染纹理上之后，其性能将像普通纹理一样 - 无论您绘制多少对象。

[source, as3]
----
var image:Image = new Image(renderTexture);
addChild(image); // <1>
----
<1> 此纹理可以像任何其他纹理一样使用。

如果你一次绘制大量对象，建议通过`drawBundled`方法将绘图调用捆绑在一个块中，如下所示。
这使得Starling可以跳过几个相当昂贵的操作，极大地加快了这个过程。

[source, as3]
----
renderTexture.drawBundled(function():void // <1>
{
    for (var i:int=0; i<numDrawings; ++i)
    {
        image.rotation = (2 * Math.PI / numDrawings) * i;
        renderTexture.draw(image); // <2>
    }
});
----
<1> 通过将所有绘图调用封装在一个函数中来激活绑定的绘图。
<2> 在此函数内部，像以前一样调用`draw`来绘制对象。

要擦除渲染纹理的某些部分，可以使用任何显示对象（如“橡皮擦”）,通过设置其混合模式为BlendMode.ERASE来达到目的。

[source, as3]
----
brush.blendMode = BlendMode.ERASE;
renderTexture.draw(brush);
----

要彻底擦除它，使用`clear`方法。

[CAUTION]
.设备丢失
====
不幸的是，渲染纹理有一个很大的缺点：当渲染设备丢失时，它们会丢失所有内容。
<<设备丢失>> 将在后面的章节中详细讨论; 简而言之，这意味着Stage3D在某些情况下可能会丢失其缓冲区的所有内容。
（是的，虽然听起来令人讨厌。）

因此，如果纹理的内容是持久的（即它不只是过眼云烟）,这个问题真的很重要，你将需要做一些额外的处理。
我们将在上述章节中探讨可能的策略 - 在这里我只是想提到这个事实，碰见它时才不会措手不及。
====

// TODO: Mipmaps
// TODO: Image color
