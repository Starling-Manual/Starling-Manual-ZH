== 事件处理

您可以将事件视为您作为程序员感兴趣的任何类型的事件。

* 例如，移动应用可能会通知您设备方向已更改，或者用户刚刚触摸了屏幕。
* 最基本的，可能表示一个按钮被触发，或一个骑士，他已经用尽了生命值。

这就是Starling的事件机制。

=== 动机

事件机制是Starling架构的一个关键特性。
简而言之，事件允许对象彼此进行通信。

你可能会想：我们已经有了一个机制 - 方法！
确实如此，但方法只能在同一个线性方向上工作。
例如，查看包含Button的MessageBox。

[ditaa, messagebox-calls-button]
....
/------------\
| MessageBox |
\-----+------/
      |
      | Method
      v
  /---+----\
  | Button |
  \--------/
....

消息框本身包含了按钮这个对象，因此可以直接使用其方法和属性，例如：

[source, as3]
----
public class MessageBox extends DisplayObjectContainer
{
    private var _yesButton:Button;

    private function disableButton():void
    {
        _yesButton.enabled = false; // <1>
    }
}
----
<1> 通过属性与Button通信。

另一方面，Button实例没有对消息框的引用。
毕竟，一个按钮可以被任何组件使用 - 它完全独立于MessageBox类。
这是一件好事，否则，你只能在消息框中使用按钮，而在其他地方就只能呵呵了!

进一步说，按钮这样设计是有原因的 - 如果触发，它需要告诉别人！
换句话说：按钮需要能够发送消息给它的所有者，无论谁。

[ditaa, button-dispatches-to-messagebox]
....
/------------\
| MessageBox |
\------------/
      ^
      | Event
      |
  /---+----\
  | Button |
  \--------/
....

=== Event & EventDispatcher

有件事我得承认：当我向你展示Starling的显示对象的类层次结构时，我省略了实际的基类：EventDispatcher。

[plantuml, class-hierarchy-with-eventdispatcher]
....
include::../defaults.plantuml[]

abstract class DisplayObject
abstract class DisplayObjectContainer

EventDispatcher <|-- DisplayObject
DisplayObject <|-- Mesh
DisplayObject <|-- DisplayObjectContainer
....

该类赋予所有显示对象分派和处理事件的能力。
所有显示对象继承自EventDispatcher,这不是巧合; 在Starling中，事件系统与显示列表紧密集成。
这有一些优点，我们将在后面看到。

最好通过看一个例子来描述事件。

想象一下，你有一只狗; 让我们叫他Einstein。
每天几次,Einstein会告诉你，他想出去散步。
他是通过吠叫来做到的。

[source, as3]
----
class Dog extends Sprite
{
    function advanceTime():void
    {
        if (timeToPee)
        {
            var event:Event = new Event("bark"); // <1>
            dispatchEvent(event); // <2>
        }
    }
}

var einstein:Dog = new Dog();
einstein.addEventListener("bark", onBark); // <3>

function onBark(event:Event):void // <4>
{
    einstein.walk();
}
----
<1> 字符串`bark`将标识事件。它封装在Event实例中。
<2> 调度`事件'将发送给订阅`bark`事件的每个人。
<3> 这里，我们通过调用`addEventListener`来订阅。第一个参数是事件`type`，第二个是`listener`（一个函数）。
<4> 当狗吠时，这个方法将调用事件作为参数。

你看到了事件机制的三个主要组件：

* 事件封装在* Event *类（或其子类）的实例中。
* 要分派事件，发送者调用* dispatchEvent *，传递Event实例。
* 要监听事件，客户端调用* addEventListener *，指示他感兴趣的事件类型以及要调用的函数或方法。

有时，你的阿姨会照顾狗。
当这种情况发生时，你不用关心狗吠 - 因为你的阿姨知道她要干什么！
所以你删除事件监听器，这是一个不仅对狗主人，对Starling开发者来说也是一个好的做法。

[source, as3]
----
einstein.removeEventListener("bark", onBark); // <1>
einstein.removeEventListeners("bark"); // <2>
----
<1> 这删除了特定的`onBark`监听器。
<2> 这将删除该类型的所有侦听器。

这么多的`bark`事件。
当然，Einstein可以分派几种不同的事件类型，例如`howl`或`growl`事件。
建议将这样的字符串存储在静态常量中，例如。 右边的“狗”类。

[source, as3]
----
class Dog extends Sprite
{
    public static const BARK:String = "bark";
    public static const HOWL:String = "howl";
    public static const GROWL:String = "growl";
}

einstein.addEventListener(Dog.GROWL, burglar.escape);
einstein.addEventListener(Dog.HOWL, neighbor.complain);
----

Starling在Event类中预定义了几个非常有用的事件类型。
这里有一些最受欢迎的类型：

* * Event.TRIGGERED：*触发了一个按钮
* * Event.ADDED：*显示对象已添加到容器
* * Event.ADDED_TO_STAGE：*显示对象已添加到连接到舞台的容器
* * Event.REMOVED：*从容器中删除显示对象
* * Event.REMOVED_FROM_STAGE：*显示对象失去其与舞台的连接
* * Event.ENTER_FRAME：*一段时间过去了，一个新的帧被渲染（我们将在后面谈到）
* * Event.COMPLETE：*某事（像一个MovieClip实例）刚刚完成

=== 自定义事件

狗吠的原因不尽相同，对吧？
Einstein可能表达他想撒尿，或者他饿了。
它也可能是一种方式告诉一只猫，是离开此地的时候了。

懂狗的人可能会听到差异（我不会，因为我是一个只懂猫的人）。
这是因为聪明的狗设置了一个BarkEvent来存储他们的意图。

[source, as3]
----
public class BarkEvent extends Event
{
    public static const BARK:String = "bark"; // <1>

    private var _reason:String; // <2>

    public function BarkEvent(type:String, reason:String, bubbles:Boolean=false)
    {
        super(type, bubbles); // <3>
        _reason = reason;
    }

    public function get reason():Boolean { return _reason; } // <4>
}
----
<1> 在自定义事件类中存储事件类型是一个很好的做法。
<2> 创建自定义事件的原因：我们要存储一些信息。 这里，这是“reason”字符串。
<3> 在构造函数中调用超类。 （我们将很快看看“冒泡”的意思。）
<4> 通过属性访问“reason”。

狗现在可以使用这个自定义事件时吠叫：

[source, as3]
----
class Dog extends Sprite
{
    function advanceTime():void
    {
        var reason:String = this.hungry ? "hungry" : "pee";
        var event:BarkEvent = new BarkEvent(BarkEvent.BARK, reason);
        dispatchEvent(event);
    }
}

var einstein:Dog = new Dog();
einstein.addEventListener(BarkEvent.BARK, onBark);

function onBark(event:BarkEvent):void // <1>
{
    if (event.reason == "hungry") // <2>
        einstein.feed();
    else
        einstein.walk();
}
----
<1> 请注意，参数是事件类型“BarkEvent”。
<2> 这就是为什么我们现在可以访问`reason`属性并执行相应地操作。

这样，任何熟悉BarkEvent的狗主人终将能够真正了解他们的狗。
实在是一项成就！

=== 简化

正如你所见：创建这个额外的类只是为了能传递那个“reason”字符串有点麻烦。
毕竟，它通常只是我们感兴趣的一个单一的信息。
不得不为这样简单的机制创建额外的类感觉有点低效。

这就是为什么你实际上不需要经常使用子类方法。
相反，你可以使用Event类的`data`属性，它可以存储任意引用（它是泛类型：Object）。

将BarkEvent逻辑替换为：

[source, as3]
----
// create & dispatch event
var event:Event = new Event(Dog.BARK);
event.data = "hungry"; // <1>
dispatchEvent(event);

// listen to event
einstein.addEventListener(Dog.BARK, onBark);
function onBark(event:Event):void
{
    trace("reason: " + event.data as String); // <2>
}
----
<1> 将reason存储在`data`属性中。
<2> 为了回到原因，将`data`转换为String。

这种方法的缺点是我们失去一些类型安全。
但在我看来，与其实现一个完整的类，我宁愿使用类型转换。

此外，Starling还有几个快捷方法，可以进一步简化此代码！
看这个：

[source, as3]
----
// create & dispatch event
dispatchEventWith(Dog.BARK, false, "hungry"); // <1>

// listen to event
einstein.addEventListener(Dog.BARK, onBark);
function onBark(event:Event, reason:String):void
{
    trace("reason: " + reason); // <2>
}
----
<1> 创建类型为“Dog.BARK”的事件，填充“data”属性，并调度事件 - 一行代码完成。
<2> `data`属性被传递给事件处理程序的（可选）第二个参数。

我们摆脱了相当多的笨重的代码！
当然，即使您不需要任何自定义数据，您也可以使用相同的机制。
让我们看看最简单的事件交互：

[source, as3]
----
// create & dispatch event
dispatchEventWith(Dog.HOWL); // <1>

// listen to event
dog.addEventListener(Dog.HOWL, onHowl);
function onHowl():void // <2>
{
    trace("hoooh!");
}
----
<1> 通过仅指定事件的类型来分派事件。
<2> 注意，此函数不包含任何参数！如果您不需要它们，则无需指定它们。

注意：简化的`dispatchEventWith`调用实际上甚至更高的内存效率，因为Starling将在后台复用Event对象。

=== 冒泡

在我们前面的例子中，事件分派器和事件监听器通过`addEventListener`方法直接连接。
但有时，这不是你想要的。

让我们假设你创建了一个带有深层显示列表的复杂游戏。
在这个列表的某个分支上，Einstein（这个游戏的主角 - 狗）陷入陷阱。
他在痛苦中嚎叫，在他的最后呼吸中，发出一个“GAME_OVER”事件。

不幸的是，这些信息需要在显示列表中，在游戏的根类中
在这样的事件中，它通常重置等级并将狗返回到其最近一个数据备份点。
将这个事件从狗身上通过多个显示对象，直到到达游戏根为止，这将是非常麻烦的。

这是一个非常常见的要求 - 这就是事件系统支持向上冒泡(bubbling)的原因。

想象一个真正的树（它是你的显示列表），并将其转动180度，使树干指向上方。
树干，这是你的舞台，树的叶子是你的显示对象。
现在，如果一个叶子创造了一个冒泡事件，那么该事件将像一杯苏打水中的气泡一样向上移动，从一个分支到另一个分支（从父母到另一个），直到它最终到达树干。

.一个事件气泡一直到舞台。
image::bubbling.png[Bubbling, 200]

沿此路由的任何显示对象都可以侦听此事件。
它甚至可以弹出泡沫，阻止它进一步旅行。所有这一切都需要设置一个事件的`bubbles`属性为true。

[source, as3]
----
// classic approach:
var event:Event = new Event("gameOver", true); // <1>
dispatchEvent(event);

// one-line alternative:
dispatchEventWith("gameOver", true); // <2>
----
<1> 传递`true`作为Event构造函数的第二个参数激活冒泡。
<2> 或者，`dispatchEventWith`使用完全相同的参数。

在任何地方沿着它的路径，你可以听这个事件，例如。 对狗，其父母或阶段：

[source, as3]
----
dog.addEventListener("gameOver", onGameOver);
dog.parent.addEventListener("gameOver", onGameOver);
stage.addEventListener("gameOver", onGameOver);
----

这个功能在许多情况下都很方便; 尤其是当通过鼠标或触摸屏进行用户输入时。

=== 触摸事件

虽然典型的台式计算机由鼠标控制，但大多数移动设备（如智能手机或平板电脑）都是用手指控制的。

Starling统一了这些输入方式，将所有“指向设备”输入视为“TouchEvent”。
这样，你不必关心游戏控制的实际输入方式。
输入设备是鼠标，手写笔还是手指：Starling将始终分派触摸事件。

如果你想支持多点触摸，第一件事：请确保在创建你的Starling实例之前启用它。

[source, as3]
----
Starling.multitouchEnabled = true;

var starling:Starling = new Starling(Game, stage);
starling.simulateMultitouch = true;
----

注意属性`simulateMultitouch`。
如果启用它，您可以使用鼠标在开发计算机上模拟多点触摸输入。
当您移动鼠标光标时，按住键盘的[Ctrl]键(Windows)或键盘的[Cmd]键(Mac）即可试用。
再按住键盘的[Shift]以更改替代光标的移动方式。

.使用鼠标和键盘模拟多点触控。
image::simulate-multitouch.png[Simulate Multitouch]

要对触摸事件（真实或模拟）做出反应，您需要侦听类型为“TouchEvent.TOUCH”的事件。

[source, as3]
----
sprite.addEventListener(TouchEvent.TOUCH, onTouch);
----

您可能已经注意到，我刚刚将事件侦听器添加到Sprite实例。
但是Sprite是一个容器类;它没有任何有形的可见表面。
仍然能触摸它吗？

是的，能 - 感谢bubbling。

要理解这个，先回想一下我们之前创建的MessageBox类。
当用户点击其文本字段时，任何监听文本字段上的触摸事件的人都必须得到通知 - 到目前为止，显而易见。
但是对于在消息框本身上监听触摸事件的人也是如此;毕竟文本字段是消息框的一部分。
即使有人监听舞台上的触摸事件，他应该也得到通知。
触摸显示列表中的任何对象同时意味着触摸舞台！

由于冒泡事件，Starling可以轻松地代表这种类型的交互。
当它检测到屏幕上的触摸时，它计算出哪个叶节点对象被触摸。
它创建一个TouchEvent并在该对象上调度它。
从那里，它将沿着显示列表冒泡,一直到根。

==== Touch Phases

Time to look at an actual event listener:

[source, as3]
----
private function onTouch(event:TouchEvent):void
{
    var touch:Touch = event.getTouch(this, TouchPhase.BEGAN);
    if (touch)
    {
        var localPos:Point = touch.getLocation(this);
        trace("Touched object at position: " + localPos);
    }
}
----

这是最基本的情况：找出是否有人触摸屏幕并描绘出坐标。
`getTouch`方法由TouchEvent类提供，并帮助您找到感兴趣的触摸。

注意：Touch类封装了单次触摸的所有信息：发生的地方，它在前一帧中的位置等。

作为第一个参数，我们将`this`传递给`getTouch`方法。
因此，我们要求它返回在'this`或其children上发生的任何触摸。

触摸在他们的生命周期内经历了一些阶段：

[horizontal]
`TouchPhase.HOVER`:: 仅用于鼠标输入;当鼠标按钮up状态时移动到对象上时调度。
`TouchPhase.BEGAN`:: 手指刚刚触摸屏幕，或者鼠标按钮被按下。
`TouchPhase.MOVED`:: 手指在屏幕上移动，或者按下按钮时鼠标移动。
`TouchPhase.STATIONARY`:: 手指或鼠标（按下按钮）自最后一帧以来未移动。
`TouchPhase.ENDED`:: 手指从屏幕提起或鼠标按钮弹起。

因此，上面的示例（寻找开始阶段'BEGAN'）将在手指触摸屏幕的准确时刻写入跟踪输出，而不是在移动或离开屏幕时。

==== 多点触控

在上面的示例中，我们只监听单次触摸（即只有一个手指）。
多点触摸的处理非常相似;唯一的区别是需要调用`touchEvent.getTouches`（注意复数）。

[source, as3]
----
var touches:Vector.<Touch> = event.getTouches(this, TouchPhase.MOVED);

if (touches.length == 1)
{
    // one finger touching (or mouse input)
    var touch:Touch = touches[0];
    var movement:Point = touch.getMovement(this);
}
else if (touches.length >= 2)
{
    // two or more fingers touching
    var touch1:Touch = touches[0];
    var touch2:Touch = touches[1];
    // ...
}
----

`getTouches`方法返回一个接触者向量。
我们可以基于该向量的长度和内容来确定逻辑。

* 在第一个if子句中，屏幕上只有一个手指。通过`getMovement`，我们可以实现拖动手势。
* 在else子句中，两个手指在屏幕上。通过访问两个触摸对象，我们可以实施捏合手势。

注意：作为Starling下载一部分的演示应用程序包含https://github.com/Gamua/Starling-Framework/blob/master/samples/demo/src/utils/TouchSheet.as[TouchSheet]类，该类使用在Multitouch场景中。
它显示了允许拖动，旋转和缩放sprite的触摸处理程序的示例实现。

==== 鼠标移开和结束悬停

当你想要检测鼠标从一个对象上移开时（鼠标按钮处于“up”状态），有一个特殊情况需要考虑。
这只与鼠标输入有关。）

如果悬停触摸的目标已更改，则会将TouchEvent分派给上一个目标，以通知它不再悬停在其上。
在这种情况下，`getTouch`方法将返回`null`。
使用这些知识来捕获什么可以称为mouseOut事件。

[source, as3]
----
var touch:Touch = event.getTouch(this);
if (touch == null)
    resetButton();
----
