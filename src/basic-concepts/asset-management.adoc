== 资源管理

现在应该弄明白一件事：纹理构成了每个应用程序资源的重要组成部分。
特别是游戏，需要很多图形;从用户界面到字符，列表细项，背景等
这还不是全部,您可能还需要管理声音和配置文件。

要引用这些资源，你有几个选择:

* 将它们嵌入到应用程序（通过`[Embed]'元数据）。
* 从磁盘加载它们（仅适用于AIR应用程序）。
* 从URL加载它们，例如来自网络服务器。

由于每种访问方式都需要编写不同的代码（取决于资源类型和加载机制），所以很难以统一的方式访问资源。
幸运的是，Starling包含一个能解决这个问题的帮助类：AssetManager。

它支持以下类型的资源：

* 纹理（来自位图或ATF数据）
* 纹理图集
* 位图字体
* 声音
* XML数据
* JSON数据
* 字节数组

为了实现这一点，AssetManager使用三步法：

1. 将资源的"指针"添加到队列中，例如`File`对象或URL。
2. 告诉AssetManager处理队列。
3. 一旦队列处理完成，您可以使用相应的“get”方法访问所有资源。

[TIP]
====
AssetManager包含一个`verbose`属性。
如果启用，则排队和加载过程的所有步骤都将输出到控制台。
这对调试非常有用，例如当您不明白为什么某些资源没有正常显示的时候！
因此，最新的Starling版本已默认启用。
====

=== 把资源加入到队列

第一步是将您要使用的所有资源加入到队列。
如何加入完全取决于每个资源的类型和来源。

==== 来自磁盘或网络的资源

把磁盘或远程服务器的**文件**添加到队列相当简单：

[source, as3]
----
// 从远程URL加载资源
assets.enqueue("http://gamua.com/img/starling.jpg");

// 从磁盘加载资源（仅限AIR）
var appDir:File = File.applicationDirectory;
assets.enqueue(appDir.resolvePath("sounds/music.mp3"));

// 递归查找此目录下的所有文件并加入到队列（仅限AIR）。
assets.enqueue(appDir.resolvePath("textures"));
----

要加载**纹理图集 **，只需将其XML文件和相应的纹理添加到队列。
需要确保XML文件中的“imagePath”属性包含正确的文件名，因为在以后创建图集时，AssetManager将会查找它。

[source, as3]
----
assets.enqueue(appDir.resolvePath("textures/atlas.xml"));
assets.enqueue(appDir.resolvePath("textures/atlas.png"));
----

**位图字体** 加载方法跟上述图集类似。
不过在这种情况下，您需要确保的是XML中的`file`属性（`.fnt`文件）设置正确。

[source, as3]
----
assets.enqueue(appDir.resolvePath("fonts/desyrel.fnt"));
assets.enqueue(appDir.resolvePath("fonts/desyrel.png"));
----

==== 嵌入资源

对于嵌入资源，我建议您将所有嵌入语句放入一个专用类中。
将它们声明为“public static const”，并遵循以下命名约定：

* 嵌入图像的类别应该与文件具有完全相同的名称，而不需要扩展名。
  这是必需的，来自XML（图集，位图字体）的引用因此才不会被打断。
* 图集XML和字体XML文件可以有任意的名称，因为它们的文件名不会被引用。

Here's a sample of such a class:

[source, as3]
----
public class EmbeddedAssets
{
    /* PNG纹理 */
    [Embed(source = "/textures/bird.png")]
    public static const bird:Class;

    /* ATF纹理 */
    [Embed(source   = "textures/1x/atlas.atf",
           mimeType = "application/octet-stream")]
    public static const atlas:Class;

    /* XML文件 */
    [Embed(source   = "textures/1x/atlas.xml",
           mimeType = "application/octet-stream")]
    public static const atlas_xml:Class;

    /* MP3声音 */
    [Embed(source = "/audio/explosion.mp3")]
    public static const explosion:Class;
}
----

当您将该类添加到任务队列后，资源管理器将稍后实例化所有嵌入其中的资源。

[source, as3]
----
var assets:AssetManager = new AssetManager();
assets.enqueue(EmbeddedAssets); // <1>
----
<1> 将“鸟”纹理，“爆炸声”和纹理图集添加到加载队列。

==== 单项资源配置

当您手动创建纹理（通过“Texture.from ...()”工厂方法）时，您有机会微调它的创建方式。
例如，您可以决定纹理格式或比例因子。

这些设置的存在一个需要注意的问题：创建纹理后，您无法再更改它们。
因此，您需要确保在创建纹理时设置正确。
资源管理器也支持这种配置：

[source, as3]
----
var assets:AssetManager = new AssetManager();
assets.textureFormat = Context3DTextureFormat.BGRA_PACKED;
assets.scaleFactor = 2;
assets.enqueue(EmbeddedAssets);
----

资源管理器将遵从这些设置创建所有纹理。
但是，这似乎只允许统一设置属性然后加载纹理，对吧？
实际上不是这样的，你可以在分开的几个步骤中，在每次调用enqueue之前分配正确的设置。

[source, as3]
----
assets.scaleFactor = 1;
assets.enqueue(appDir.resolvePath("textures/1x"));

assets.scaleFactor = 2;
assets.enqueue(appDir.resolvePath("textures/2x"));
----

这将使来自“1x”和“2x”文件夹的纹理分别使用1和2的缩放因子。

=== 加载资源

现在资源队列已经添加完成了，您可以一次加载所有资源。
根据您正在加载的资源的数量和大小，这可能需要一段时间。
因此，最好向用户展示某种进度条或加载指示符。

[source, as3]
----
assets.loadQueue(function(ratio:Number):void
{
    trace("Loading assets, progress:", ratio);

    // when the ratio equals '1', we are finished.
    if (ratio == 1.0)
        startGame();
});
----

请注意，“startGame”方法是您必须自己实现的;这时你就可以隐藏加载画面并开始实际的游戏。

如果启用了“verbose”属性，您将看到可供访问的资源名称：

----
[AssetManager] Adding sound 'explosion'
[AssetManager] Adding texture 'bird'
[AssetManager] Adding texture 'atlas'
[AssetManager] Adding texture atlas 'atlas'
[AssetManager] Removing texture 'atlas'
----

[NOTE]
====
你注意到了吗？
在最后一行，在创建纹理图集之后，实际上删除了“atlas”纹理。
这是为什么？

一旦创建了图集，您就不再需要对图集纹理感兴趣，因为它们已经被包含在子纹理中了。
因为另外提供了访问纹理的入口,因此可以去除内存中实际的图集纹理。
位图字体也是如此。
====

=== 访问资源

现在队列完成处理了，您可以使用AssetManager的各种“get ...”方法访问您的资源。
每个资源都由一个名称来引用，它是资源的文件名（不带扩展名）或嵌入对象的类名。

[source, as3]
----
var texture:Texture = assets.getTexture("bird"); // <1>
var textures:Vector.<Texture> = assets.getTextures("animation"); // <2>
var explosion:SoundChannel = assets.playSound("explosion"); // <3>
----
<1> 首先搜索独立命名的纹理，然后搜索图集纹理。
<2> 搜索与上述相同，但返回从给定的字符串开始的所有（子）纹理。
<3> 播放声音并返回控制它的SoundChannel。

如果您之前已经将位图字体加入到队列中了，那么队列完成后它将被注册并可以使用。

[TIP]
====
在我的游戏中，我通常会在我的根类上存储资源管理器的引用，并且可以通过`static`属性访问。
这使得我非常容易从游戏中的任何地方访问资源，只需通过调用“Game.assets.get ...()”(假定根类称为“Game”)。
====

// TODO: Restoring the Assets

